# 面板数据

面板数据可以理解为时间序列数据+截面数据

```stata
tsset 截面变量 时间序列变量
xtdes
```





## 动态面板与静态面板

如果解释变量（$x$）包含被解释变量的滞后值（$y_{t-i}$），则为“**动态面板**”，反之为“**静态面板**”





## 平衡面板与非平衡面板

面板数据中，如果每个时期的样本个数相同，则为“**平衡面板**”，反之为“**非平衡面板**”





## 长面板与短面板

大 N 小 T：**短面板**，主要考虑截面的性质，如**异方差问题**

大 T 小 N：长面板，主要考虑**时间序列**的性质，如**序列相关性问题**





## 固定效应模型

实质上就是在传统的线性回归模型中加入 N-1 个虚拟变量，使**每个截面都有自己的截距项**，截距项的不同反映了个体的某些不随时间改变的特征

```stata
xtreg y x, fe
	# fe：固定效应
```

Stata 的估计方法：如果截面的个数非常多，那么采用虚拟变量的方式运算量过大，因此要寻求合理的方式去除掉个体效应，因为我们关注的是 x 的系数，而非每个截面的截距项
$$
y_{it} = u_i + x_{it}b + e_{it}\tag{1}
$$
$u_i$ ：截距项的差异，即个体效应；$e_{it}$ ：干扰项

对每个截面求时间上的平均值（组内平均）：
$$
y_{mi} = u_i + x_{mi}b + e_{mi}\\
\tag{2}
$$
再求样本平均：
$$
y_{m} = u_m+x_{m}b + e_{m}\\
\tag{3}
$$
（1）-（2）得：
$$
(y_{it} - y_{mi}) = (x_{it} - x_{mi})b + (e_{it} - e_{mi}) \tag{4}
$$
为了加上截距项（方便估计 $R^2$），（4）+（3）得：
$$
(y_{it}-y_{mi}+y_m) = u_m + (x_{it}-x_{mi}+x_m)b + (e_{it}-e_{mi}+e_m)
$$
重新表示为：
$$
Y_{it} = a_0 + X_{it}b + E_{it}
$$
对该模型执行 OLS 估计，即可得到 $b$ 的无偏估计量

### 结果解释

#### $R^2$

1. 截距相同时（Pooled OLS）：
   $$
   y_{it} = a_0 + x_{it}b_o + e_{it}\tag{1}
   $$

2. 截距不同（固定效应）（Within  Estimator）：
   $$
   y_{it} = u_i + x_{it}b_w + e_{it}\tag{2}
   $$

3. 使用组内平均值估计（Between Estimator）：
   $$
   y_{mi} = a_0 + x_{mi}b_b + e_{mi}\tag{3}
   $$

根据回归估计不同，得到不同的 $R^2$

1. **Within**：模型 (2) 对应的 $R^2$，是**真正意义上的 $R^2$**
2. **Between**：$Corr(x_{mi}b_w,\ y_{mi})^2$ 使用固定效应得到的 $b_w$ 得到组间的拟合值，与组内平均实际值的相关系数的平方就是组间 $R^2$
3. **Overall**：$Corr(x_{it}b_w,\ y_{it})^2$ 使用固定效应得到的 $b_w$ 得到组间的拟合值，与实际值的相关系数的平方就是组间 $R^2$



#### F

第一个自由度是解释变量的个数，第二个自由度是 N-解释变量个数-虚拟变量个数（时间序列个数）



#### rho

$rho=(sigma\_u)^2 / ((sigma\_u)^2 + (sigma\_e)^2)$

反映个体效应的波动占整个波动的比例



#### 个体效应（固定效应）的显著性

最下面一行的 "Prob > F = 0.0000" 越小越显著，即固定效应高度显著



#### 调整后的 $R^2$ $adj-R^2$

```stata
areg y x1 x2, a(截面变量)
```



#### 截距项

**方法一**：加入 (N-1) 个虚拟变量，采用 OLS 估计

可以同时获得每个截距项的标准误、t 值和 p 值，适用于 大 T 小 N 型数据

```stata
tab 截面变量, gen(dum)
reg y x1 x2 dum*, nocons
	# 可选择添加 robust
```

**方法二**：xi 前缀

```stata
xi: reg y x1 x2 i.id
xi: reg y x1 x2 i.id, robust
```

**方法三**：predict 命令

```stata
qui xtreg y x1 x2, fe
predict a, u
	# u：得到截距项
replace a = _b[_cons] + a
duplicates example 截面变量 a
order 截面变量 时间序列变量 a
```

**方法四**：areg 命令

```stata
areg y x1 x2, absorb(截面变脸)         
predict ai, d
	# 得到截距
gen a_i = ai + _b[_cons]
order 截面变量 时间序列变量 a_i ai
```

**方法五**：fese 命令

```stata
fese y x1 x2, s(o) oonly
```



#### 拟合值和残差

```stata
xtreg y x1 x2 x3, fe
predict y_hat
	# 默认得到拟合值，即 xb
predict a, u
	# 得到个体效应，即每个截面的截距项
predict res, e
	# 得到残差
predict cres, ue
	# 得到残差和个体效应的和
```





## 随机效应模型

**基本思想**：将**随机干扰项**分成两种，一种是不随时间改变的，即个体效应 $u_i$，另一种是随时间改变的，即通常意义上的干扰项 $e_{it}$，简言之，认为**个体效应是随机的，都反应在干扰项里**

假设：
$$
Corr(u_i,X)=0
$$
如果违反该假设，随机效应就会有内生性的严重问题
$$
\begin{aligned}
y_{it} &= x_{it}b + (u_{i}+e_{it})\\
&=x_{it}b+v_{it}\\
\end{aligned}
$$
估计方法：GLS
$$
Var(v_{it}) = \sigma_u^2 + \sigma_e^2\\
Cov(v_{it},v_{is}) = \sigma_u^2\ \ \ \  \text{固定效应不随时间而改变} \\ 
Cov(v_{it},v_{js}) = 0 \ \ \ \  \text{不同公司的干扰项不相关}
$$
利用 Between Estimator 拿掉  $u_i$ 估计出 $e_{it}$ 的方差，然后利用联立方程，得出 $u_i$ 的方差，进而采用 GLS或 FGLS

**随机效应估计量是固定效应估计量和组间效应估计量的加权平均**
$$
y_{r_{it}} = y_{it} - \theta y_{mi}\\
x_{r_{it}} = x_{it} - \theta x_{mi}\\
\theta = 1 - \sigma_u / \sqrt{(T\sigma_a^2 + \sigma_u^2)}  
$$

```stata
xtreg y x1 x2 x3, re
	# fe：随机效应
```

### 结果解释

#### $R^2$

因为随机效应模型采用的是 GLS 估计，GLS 估计不能保证转换后的模型仍存在常数项，于是 $R^2$ 就没有太大意义

- Within：$Corr(x_{it}-x_{mi}b_r,\ y_{it}-y_{mi})^2$
- Between：$Corr(x_{mi}b_r,\ ym_i)^2$
- Overall：$Corr(x_{it}b_r,\ y_{it})^2$



#### rho

$rho=(sigma\_u)^2 / ((sigma\_u)^2 + (sigma\_e)^2)$

反映个体效应的波动占整个波动的比例





## 时间效应

**基本思想**：固定效应模型加入了不同截面的虚拟变量，时间效应考虑不同时间，加入时间虚拟变量

单向固定效应：
$$
y_{it} = u_i + x_{it}b + e_{it}
$$

### 双向固定效应（加入时间虚拟变量）

$$
y_{it} = u_i + f_t+x_{it}b + e_{it}\tag{1}
$$

```stata
qui tab 时间变量, gen(yr)
drop yr1
	# 避免多重共线性
xtreg y x1 x2 x3 x4 yr*, fe
```



### 随机效应模型中的时间效应

```stata
xtreg y x1 x2 x3 x4 yr*, re
```



### 时间效应显著性检验

#### Wald 检验

```stata
xtreg y x1 x2 x3 x4 yr*, fe
test yr2 = yr3 = yr4 = yr5 = yr6 = yr7
test yr2 = yr3 = yr4 = yr5 = yr6 = yr7 = 0
```

若 p 值很小，表明显著不为 0，则说明存在时间效应



#### LR 检验

```stata
xtreg y x1 x2 x3 x4, fe
est store fe
xtreg y x1 x2 x3 x4 yr*, fe
est store fe_dumt
lrtest fe fe_dumt
```

若 p 值很小，则说明存在时间效应





## 模型的筛选

### 固定效应模型还是 Pooled OLS

**基本思路**：检验截面的个体效应 $u_i$ 是否显著

#### 方法 1：Wald 检验*

```stata
xtreg y x1 x2 x3 x4, fe
```

根据最后一行的 F test that all u_i=0 的 p 值，如果 p 值很小，则认为存在固定效应



#### 方法 2：LR 检验

```stata
qui tab 截面变量, gen(dum)
reg y x1 x2 x3 x4
est store m_ols
reg y x1 x2 x3 x4 dum*, nocons  
est store m_fe
lrtest m_ols m_fe
```

若 p 值很小，则认为存在固定效应



### 随机效应模型还是 Pooled OLS

**基本思路**：检验干扰项的方差是否等于 0
$$
H_0:Var(u) = 0
$$

#### 方法 1：B-P 检验

```stata
xtreg y x1 x2 x3 x4, re
xttest0
```

若 p 值很小，则说明存在随机效应



#### 方法 2：LR test（似然比检验）

```stata
xtreg y x1 x2 x3 x4, re mle
```



### 固定效应模型还是随机效应模型

#### 方法 1：Hausman 检验

$$
y_{it} = u_i + x_{it}b + e_{it}
$$

**基本思路**：固定效应与随机效应的区别在于个体效应 $u_i$ 在随机效益中被视作随机干扰项，而干扰项与解释变量不相关，如果符合这一条件，固定效应与随机效应都是一致的，但随机效应更有效；反之，固定效应任有效，但随机效应有偏

原假设：固定效应与随机效应不存在差异

```stata
xtreg y x1 x2 x3 x4, fe
est store fe
xtreg y x1 x2 x3 x4, re
est store re
hausman fe re
```

p 值越小，越认为存在差异，应采用固定效应模型



**Hausman 检验值为负**：通常是因为随机模型的基本假设 $Corr(x,u_i)=0$ 无法得到满足

```stata
hausman m_fe m_re, sigmaless
	* 检验过程中两个模型都采用固定效应模型的方差-协方差矩阵
hausman m_fe m_re, sigmamore
	* 检验过程中两个模型都采用随机效应模型的方差-协方差矩阵
```

p 值越小，越应采用固定效应模型



**如果 Hausman 检验拒绝随机效应模型**：

1. 使用固定效应模型
2. 使用工具变量法（IV）



#### 方法 2：过度约束检验

**基本思想**：如果固定效应和随机效应都是一致的，那么系数估计值应当不存在显著差异

```stata
xtreg y x1 x2 x3 x4, re
xtoverid
```

p 值越小，越应采用固定效应模型





## 组间异方差检验

### 固定效应模型

```stata
xtreg y x1 x2 x3 x4, fe
xttest3
```

p 值越小，越拒绝无异方差的原假设



### 随机效应模型

随机效应模型本身已经较大程度的考虑了异方差问题，主要体现在 $\sigma_u^2$ 上





## 序列相关检验

### 固定效应模型

#### 1. Wooldridge

若无序列相关，则一阶差分后残差相关系数应为 -0.5

```stata
xtserial y x1 x2 x3 x4
```

原假设：无一阶自相关



#### 2. Arellano-Bond 检验

```stata
reg y x1 x2 x3, cluster(截面变量)
abar
	/* 不考虑个体特征，只考虑截面相关性 */
	
reg y x1 x2 x3 dum*, nocons 
abar
	/* 不考虑截面相关性，只考虑个体特征 */
```



### 随机效应模型

```stata
xtreg y x1 x2 x3, re
xttest1
	/*提供多个统计检验量*/
```





## 截面相关检验

#### 1. xttest2 命令

原假设：所有截面残差的相关系数都相等

```stata
xtreg y x1 x2 x3, fe
xttest2
```

由于检验过程中执行了 SUE 估计，所以要求 T>N



### 2. xtcsd 命令

```stata
xtreg y x1 x2 x3, fe
xtcsd, pesaran     
	/* Pesaran(2004) */
xtcsd, friedman    
	/* Friedman(1937) */
xtcsd, frees       
	/* Frees(1995) */
xtcsd, pesaran show
	/* show：显示截面之间的相关系数 */
```

截面较多时，该方法耗时较长





## 异方差的处理

### 1. 异方差稳健型估计

```stata
xtreg y x1 x2 x3 x4, fe robust  
est store fe_rb
```



### 2. 采用Bootstrap标准误

优点：统计推断并不依赖具体的分布假设

原理：

固定效应模型为：
$$
y_{it} = u_i + x_{it}\cdot b + v_{it}\tag{1}
$$
估计完（1）后，将得到 $b$ 和 $u_i $ 的估计值设为 $b_0$ 和 $u_{0_i}$ ，则：
$$
y_{bs_{it}} = u_{0_i} + x_{it}\cdot b_0 + v_{bs_{it}}\tag{2}
$$
估计（2），得到 $b_{bs_1}, b_{bs_2},......b_{bs_300}$ ，计算这 300 个系数的标准差，便可以得到系数 $b$ 的标准误

```stata
xtreg y x1 x2 x3 x4, fe vce(bootstrap, reps(重复次数)) 
est store fe_bs
```





## 序列相关的处理

### 一阶自相关

#### 1. xtregar 命令

模型：
$$
y_{it} = u_i + x_{it}\cdot b + v_{it}\\
v_{it} = \rho\cdot v_{it-1} + z_{it}
$$

```stata
xtregar y x1 x2 x3 x4, fe 
est store fe_ar1

xtregar y x1 x2 x3 x4, fe lbi 
	/* lbi：提供 Baltagi-Wu LBI test 统计量 */
	
xtregar y x1 x2 x3 x4, re    
est store re_ar1 
```

这里的 Durbin-Watson 统计量具有较为复杂的分布，不同于时间序列中的 D-W 统计量，其临界值见 Bhargava et al. (1982, The Review of Economic Studies 49:553-549)

Baltagi-Wu LBI 统计量基本上没有太大的参考价值，因为他们并未提供临界值表，而该统计量的分布又相当复杂



#### 2. 两阶段估计

通过第一阶段拟合的一阶自相关系数，代回原模型中作为初始值再次做回归

```stata
xtregar y x1 x2 x3 x4, fe twostep
est store fe_ar1_two
```



#### 选择一阶相关系数的计算方法

```stata
xtregar y x1 x2 x3 x4, fe rhotype(tscorr)
	/* 指定一阶自相关系数的估计方法为：残差的当期值和滞后一阶的相关系数（协方差/标准差的乘积） */ 
est store fe_ar1_tscorr
```



#### 结果对比

```stata
xtreg y x1 x2 x3 x4, fe

est store fe
local models "fe fe_ar1 re_ar1 fe_ar1_two fe_ar1_tscorr"
esttab `models', b(%6.3f) se(%6.3f) mtitle(`models') r2 sca(r2_w corr)
```



注意：

- 面板数据分析中，除非 T 很大，通常没有必要列出 D-W 值
- 虽然能按照上述方法列出 D-W 值，但一定要给出临界值，否则没有参考意义，因为其分布与传统的 D-W 迥异



### 高阶自相关

高阶自相关不是很常见，主要存在于 N 很少，T 很长的面板数据中

#### 1. newey2 命令

```stata
newey2 y x1 x2 x3 x4, lag(滞后阶数)  

neweyvif 
	/* 得到膨胀因子(VIF) */
```

如果每一个解释变量的膨胀因子都小于 10，通常认为不存在多重共线性问题



#### 利用 newey2 命令得到膨胀因子 (VIF)

```stata
newey2 y x1 x2 x3 x4, lag(0)
	/* 纯粹为了利用 newey2 命令得到膨胀因子 */
neweyvif
```



#### 2. xtivreg2 命令

同时考虑异方差和序列相关

```stata
xtivreg2 y x1 x2 x3 x4, fe bw(2) robust small
	/*  fw()：参数只要大于 0 就行 */
```





## 截面相关（组间相关）的处理

### cluster 选项

```stata
xtreg y x1 x2 x3 x4, fe cluster(截面变量)

xtreg y x1 x2 x3 x4, re cluster(截面变量)
```





## 综合处理

### xtscc 命令

当异方差、序列相关以及截面相关性质未知时，推荐使用该方法

xtscc 相当于 White / Newey 估计扩展到面板的情形

```stata
xtscc y x1 x2 x3 x4, fe 
	/* 同时考虑异方差和截面相关 */

xtscc y x1 x2 x3 x4, fe lag(1)
	/* 同时考虑异方差、序列相关和截面相关 */
```





## 多方程模型

适用于“**大 T，小 N**”，此时截面的异质性并不是重点关注的，而**时序特征则较为明显**，因此，模型设定中未考虑个体效果

模型：
$$
\boldsymbol{y} = \boldsymbol{Xb} + \boldsymbol{U}
$$
重点在于考虑干扰项 $\boldsymbol{U}$ 的结构，包括：

1. 异方差
2. 序列相关
3. 截面相关



### 1. 截面异方差

$$
E[e_ie_i^T] = s_i^2
$$

$i$ ：表示不同的截面



### 2. 截面相关

$$
E[e_ie_i^T] = s_{ij}^2
$$



### 3. 序列相关

$$
E[e_ie_i^T] = s_i^2M_i
$$

$M_i$ ：每个截面内部 T 个观察值构成的自相关方差-协方差矩阵



### GLS 估计

$$
b=(X^TV^{-1}X)^{-1}(X^TV^{-1}y)\\
Var(b)=(X^TV^{-1}X)^{-1}
$$



### Stata 估计和检验

#### 1. xtgls 命令

```stata
xtgls y x1 x2 x3 x4, panels(iid)   
	/* iid, 异方差、序列相关和截面相关都不考虑，等同于 Pooled OLS */
est store g_0
reg y x1 x2 x3 x4
	/* 等同于第一条命令 */
est store g_ols

xtgls y x1 x2 x3 x4, panel(het)
	/* 截面异方差 */
est store g_phet

xtgls y x1 x2 x3 x4, corr(ar1)     
	/* 所有截面具有相同的自相关系数 */
est store g_par1

xtgls y x1 x2 x3 x4, corr(psar1)   
	/* 每个截面有自己的自相关系数 */
est store g_psar1

xtgls y x1 x2 x3 x4, panel(corr)   
	/* 截面间相关且异方差 */
est store g_pcorr

xtgls y x1 x2 x3 x4, p(c) corr(ar1)
	/* 同时考虑异方差、序列相关和截面相关 */
est store g_all
```



#### 异方差的检验

```stata
xtgls y x1 x2 x3 x4, panel(het)    
	/* 截面异方差 */
xttest3
```



#### 序列相关的检验

```stata
xtserial y x1 x2 x3 x4
```



#### 截面相关的检验

```stata
xtgls y x1 x2 x3 x4, panel(het)
xttest2
```



#### 固定效应与 xtgls

- 固定效应中使用 n-1 个虚拟变量反映 n 个截面个体特征的差别

- xtgls 中没有使用虚拟变量，假设各截面的截距项相同（平均水准相同），唯一的差别反应在干扰项上（异方差、序列相关、截面相关）



#### xtgls 不汇报 $R^2$

由于 GLS 有权重的设定，很难保证转换后的模型仍有常数项，如果模型没有常数项，则 $E(\epsilon)\ne0$ ，$R^2$ 可能大于 1，也可能小于 0，从而失去意义



#### 2. xtpcse 命令

估计方法：OLS 或 Prais-Winsten 回归，更适于方块面板 N 不大 (10-20)，T 不大 (10-40)

与 xtgls 的区别：xtgls 采用 GLS 进行估计，而 xtpsce 采用 OLS 进行估计

```stata
xtpcse  y x1 x2 x3 x4
	/* OLS 估计，同时调整异方差和截面相关后计算标准误 */
est store pcse_full

xtpcse y x1 x2 x3 x4, correlation(ar1)
	/* Prais_Winsten 估计，假设有共同的自相关系数 */
est store pcse_ar1

xtpcse y x1 x2 x3 x4, correlation(ar1) rhotype(tscorr)
	/* 指定自相关系数的计算方法 */
est store pcse_tscorr   

xtpcse y x1 x2 x3 x4, correlation(ar1) hetonly
	/* 只考虑截面异方差，不考虑截面相关 */
est store pcse_ar1      
```

当 N 较大时，采用该方法会非常费时，因为方差协方差矩阵是采用 OLS 估计的残差计算的





## 内生性问题

模型：
$$
y_{it} = a_i + z_{it}b_1 + x_{it}b_2 + e_{it}
$$
$z_{it}$ ：严格外生变量

$x_{it}$ ：可能存在内生问题的变量

一般意义的内生性问题：
$$
Corr(x_{it},e_{it}) \ne 0
$$
面板特有的内生性问题：
$$
Corr(x_{it},u_{i}) \ne 0
$$



## 1. $Corr(x_{it},e_{it}) \ne 0$

一般情形下的内生性问题，Fe 和 Re 估计量都是非一致的

```stata
xtivreg y x1 x2 x3 (x4 = x5 x6 L.x4), fe
```



### 内生性的检验

#### 1. Davidson-MacKinnon 检验*

H0：OLS 和 xtivreg 都是一致的，即内生性问题对 OLS 的估计结果影响不大

```stata
xtivreg y x1 x2 x3 (x4 = x5 x6), fe
est store fe_iv
dmexogxt
```



#### 2. Hausman-Wu 检验

```stata
xtreg y x1 x2 x3 x4 x5 x6, fe
est store fe

hausman fe fe_iv
hausman fe fe_iv, sigmamore
hausman fe fe_iv, sigmaless
```

Hausman 检验的主要局限在于可能产生小于零的卡方值，这通常是拒绝原假设的表现，即认为存在内生性问题



### 工具变量的合理性检验

#### 过度识别检验

**基本思想**：检验工具变量的合理性，即与内生变量相关，而与干扰项不相关

**原假设**：选用的工具变量是合理的

```stata
xtivreg y x1 x2 x3 (x4 = x5 x6), fe
xtoverid
```



#### 识别不足检验

**原假设**：存在识别不足问题，即工具变量与内生性变量不相关

```stata
xtivreg2 y x1 x2 x3 (x4 = x5 x6), fe
```

Anderson canon. corr. LR statistic (underidentification test) :

> 即使不存在识别不足问题，但仍然可能存在弱工具变量问题



#### 弱工具变量检验

**原假设**：工具变量与内生变量有较强的相关性

```stata
xtivreg2 y x1 x2 x3 (x4 = x5 x6), fe
```

Cragg-Donald F statistic (weak identification test) ：



#### 工具变量的选择

滞后项经常被用作工具变量



### 存在异方差的 IV 估计

#### 1. Bootstrap 法

```stata
xtivreg y x1 x2 x3 (x4 = x5 x6 L.x4), fe vce(bootstrap, rep(500))
	/* 使用 Bootstrap 获得标准误 */
```



#### 2. 异方差稳健型估计

```stata
xtivreg2 y x1 x2 x3 (x4 = x5 x6 L.x4), fe robust
	/* 异方差稳健型估计 */
```





### 存在序列相关的 IV 估计

```stata
gen Lag1_x4 = L.x4
gen Lag2_x4 = L2.x4
newey2 y x1 x2 (x3 = Lag1_x4 Lag2_x4) x5, lag(2) 
```





## 2. $Corr(x_{it},u_{i}) \ne 0$

此时，固定效应模型仍然是一致的，随机效应模型不是

通常，通过 Hausman 检验，随机效应模型的原假设无法满足，但又想分析不随时间改变的因素（如性别、种族、行业特征等），**固定效应模型会把不随时间改变的变量都丢掉**，此时必须用随机效应模型



### 处理方法

#### Hausman-Taylor Estimation

模型：
$$
y_{it} = X1_{it}b_1 + X2_{it}b_2 + Z1_ib_3 + Z2_ib_4 + u_i + v_{it}
$$
$X1_{it}$ ：严格外生变量，随时间改变

$X2_{it}$ ：内生变量，随时间改变

$Z1_i$ ：严格外生变量，不随时间改变

$Z2_{i}$ ：内生变量，不随时间改变

估计方法：

1. 使用固定效应模型，得到 $b_1,b_2$ 的估计值和残差 $e_{it}$
2. 使用最小二乘法用  $e_{it}$ 对 $Z1_i$ 和 $Z2_{i}$  做回归（用 $X1_{it}$ 和 $Z1_i$ 做工具变量），得到 $b_3,b_4$ 的估计值
3. 利用上述 $b_1,b_2,b_3,b_4$ 的估计值，得到相应的残差向量，进而计算出方差-协方差矩阵，进行统计推断

```stata
xtsum x21 x22 x23 x24 x25
	/* 随时间改变的个体随机效应 */
	
correlate z11 z12 x11 x12 x13 x14 z21
	/* 用严格外生变量作为不随时间改变的内生变量（z21）的工具变量 */

xthtaylor y x11 x12 x13 x14 x21 x22 x23 x24 x25 z11 z12 z21, endog(x21 x22 x23 x24 x25 z21)
	/* endog：指定内生变量，其他变量作为内生变量的工具变量 */
est store re_htaylor
```

结果解释：

1. 各种类型的变量分开列示（TV：随时间改变的变量；TI：不随时间改变的变量）
2. rho = 0.97  表明 $u_i$ 在残差的波动中占有很大的比例

**注意**：查看相关性很重要 `correlate`



### 检验

```stata
xthtaylor y x11 x12 x13 x14 x21 x22 x23 x24 x25 z11 z12 z21, endog(x21 x22 x23 x24 x25 z21)
xtoverid  
	/* 检验工具变量的合理性 */
xtoverid, robust
	/* 使用稳健型标准误，检验工具变量的合理性 */
xtoverid, cluster(id)
	/* 考虑截面相关，检验工具变量的合理性 */
```





## 面板随机系数模型

模型：
$$
y_i = X_ib_i + u_i
$$
每个截面的**系数**分解为所有截面的均值+个体效应的效果
$$
b_i = b + v_i\\
E(v_i) = 0  \\
E(v_iv_i^T) = \Sigma
$$
代入得：
$$
\begin{aligned}
y_i &= X_i(b + v_i) + u_i\\
&=X_ib+(X_iv_i+u_i)\\
&=X_ib+w_i
\end{aligned}
$$
其中：
$$
E(w_i) = 0  \\
E(w_iw_i^T) = \Pi\ \ \ \ \text{方差-协方差矩阵}
$$
估计方法：GLS
$$
y = Xb + W\\
\hat{b} = \sum{W_ib_i}
$$
$W_i$ ：权重矩阵

$b_i$ ：是对单个截面执行 OLS 得到的估计值

随机系数模型的估计值事实上是单个截面 OLS 估计的加权平均（权重包含了个体效应）



### 估计似无相关（SUE）模型

事实上是单个截面的 OLS 估计，只是考虑了截面相关性

```stata
reshape wide y x1 x2, i(时间序列变量) j(截面变量)
	/* 转换为列名 y1 x11 x21 y2 x12 x22 y3 x13 x23... 的形式*/
sureg (y1 x11 x21) (y2 x12 x22) (y3 x13 x23) (y4 x14 x24) (y5 x15 x25)
	/* 所有截面单独做 OLS 回归得到系数，方差-协方差矩阵用所有截面的方差-协方差联合起来得到 */
```

若结果显示每个截面的系数并不完全相同，则考虑用随机系数模型



### 估计随机系数模型

```stata
use xxx.dta, clear
xtrc y x1 x2, i(截面变量)
est store xt_rc
```

结果汇报的 $b$ 是平均的系数

```stata
xtrc y x1 x2, i(截面变量) betas
	/* betas：汇报每个截面的系数值 */
```





## 面板随机边界分析

### 截面模型

$$
y_i = b_0 + x_ib_1 + v_i - u_i\\v_i \sim N(0, \sigma_v^2)\\u_i \sim N^+(E_u, \sigma_u^2)
$$

$v_i$ ：一般意义上的随机干扰项

$u_i$ ：反应无效率的干扰项，具有半正态分布



### 面板模型 1：无效率成分不随时间改变

$$
y_{it} = b_0 + x_{it}b_1 + v_{it} - u_{it}\\v_{it} \sim N(0, \sigma_v^2)\\u_{it}=u_i\\u_i \sim N^+(\mu, \sigma_u^2),\ \ \mu=E(u_{it})
$$

```stata
xtfrontier y x1 x2, ti
	/* ti：不随时间改变 */
est store xtsfa_ti
```

结果分析：

- sigma_u2 = $\sigma_u^2 $

* sigma_v2 = $\sigma_v^2$
- sigma2  = $\sigma_u^2 + \sigma_v^2$
- gamma = $\sigma_u^2 / (\sigma_u^2+\sigma_v^2)$   即无效率的波动占总波动的比例
- /ilgtgamma = $1/logit(\gamma)$ ， $logit(\gamma) = \gamma/(1-\gamma)$
- /lnsigma2, /ilgtgamma 之所以做转换，都是为了方便求解



### 面板模型 2：无效率成分随时间改变

$$
y_{it} = b_0 + x_{it}b_1 + v_{it} - u_{it}\\v_{it} \sim N(0, \sigma_v^2)\\u_{it}=e^{-\eta (t-T_i)u_i}\\u_i \sim N^+(\mu, \sigma_u^2),\ \ \mu=E(u_{it})
$$

$t$ ：当期时间

$T_i$ ：每个截面的观察期数

```stata
xtfrontier y x1 x2, tvd
est store xtsfa_tvd
```

结果分析：如果 $\eta$ 的估计值非常接近 0，可以施加约束条件

```stata
constraint define 1 [eta]_cons = 0

xtfrontier y x1 x2, tvd constraints(1)
est store xtsfa_tvd_eta0
```



### 技术效率的估计

- xb ：y 的拟合值
- u ： $-ln{E(u_{it}|v_{it})}$
- m ： $-ln{M(u_{it}|v_{it})}$ ，$M(u_{it}|v_{it})=-u_i,\ \ if\ u_i\ge0;\ \ 0,\ \ otherwise$
- te ： $E[exp(-u_{it}|v_{it})]$

```stata
xtfrontier y x1 x2, ti
predict yhat    
predict eff_te, te   /* 一般应用此项，因为 eff_te 介于 0 和 1 之间 */
predict eff_u , u
predict eff_m , m
sum eff_*

gen eff = 1 - eff_te
histogram eff_te
histogram eff
```





## 动态面板模型

模型：
$$
y_{it} = a_0y_{i(t-1)} + a_1x_{it} + a_2w_{it} + u_i + e_{it}
$$
特点：解释变量中包含了被解释变量的一阶滞后项，可以是非平衡面板，但要保证时间连续

- $x_{it}$ ：严格外生变量，$E(x_{it},e_{is}) =0,\ \ for\ \ all\ \ t\ and\ \ s$，即所有干扰项与 x 都不相关
- $w_{it}$ ：先决变量，$E(w_{it},e_{is})\ne0,\ \ if\ \ s<t;\ \ E(w_{it},v_{is})=0,\ \ if\ \ s\ge t$，即前期干扰项与当期 x 相关，但当期和未来期干扰项与 x 不相关
- $y_{i(t-1)}$ ：内生变量，$E(x_{it},e_{is})\ne0,\ \ if\ \ s\le t$ ，即前期和当期（尤其是当期干扰项）与 x 相关
- $u_i$ ：随机效应，在截面间是 i.i.d. 的，$u_i$ 与 $e_{it}$ 独立



### 内生性问题

1.  若假设 $u_i$ 为随机效应，则 $Corr(y_{i(t-1)}, u_i)\ne0$ ，因为 $y_{it}$ 与 $u_{i}$ 相关，而 $u_{i}$ 不随时间改变，所以 $y_{i(t-1)}$ 与 $u_{i}$ 相关
2. 若假设 $u_i$ 为个体效应，需要想办法去除，因为数据为 "大 N 小 T"



#### 一阶差分

$$
\Delta y_{i(t-1)} = y_{i(t-1)} - y_{i(t-2)}\\\Delta e_{it}=e_{it}-e_{i(t-1)}
$$

显然 $y_{i(t-1)}$ 与 $e_{i(t-1)}$ 相关

因而，**一阶差分能够去除个体效应，但无法解决内生性问题**



### 组内去心

$$
ym_{i(t-1)} = y_{i(t-1)} - \frac{1}{T-1}(y_{i(t-1)}+\dots+y_{iT})\\em_{it} = e_{it} - \frac{1}{T}(e_{it}+e_{i(t-1)}+\dots+e_{iT})
$$

> 没看懂

因为 $ym_{i(t-1)}$ 和 $em_{it}$ 里包含 $y_{i(t-1)}$ 与 $e_{i(t-1)}$ ，所以 $ym_{i(t-1)}$ 和 $em_{it}$ 仍存在内生性问题





## 动态面板内生性的处理

### 1. 一阶差分 IV 估计量

基本思想：采用一阶差分去除个体效应 $u_i$，**y 的三阶及三阶以上滞后项**都可以作为 $\Delta y_{i(t-1)}$ 的工具变量，同时，$\Delta y_{i(t-2)}$ 也可以作为 $\Delta y_{i(t-1)}$ 的工具变量

模型：
$$
\begin{aligned}y_{it}=&b_1y_{i(t-1)}+b_2y_{i(t-2)}\\&+b_3x1_{it}+b_4x1_{i(t-1)}\\&+b_5x2_{it}+b_6x2_{i(t-1)}+b_7x2_{i(t-2)}\\&+b_8x3_{it}+b_9x3_{i(t-1)}+b_{10}x3_{i(t-2)}\end{aligned}
$$

```stata
xtivreg y L2.y  x1 L1.x1  x2 L1.x2 L2.x2  x3 L1.x3 L2.x3  时间虚拟变量 (L.y = L3.y), fd
```

可以简写为：

```stata
xtivreg y L2.y L(0/1).x1 L(0/2).(x2 x3) 时间虚拟变量 (L.y = L3.y), fd
```



### 2. 一阶差分 GMM 估计量

模型：
$$
y_{it} = a_0y_{i(t-1)} + a_1x_{it} + a_2w_{it} + u_i + e_{it}
$$
假设：干扰项 $e_{it}$ 不存在序列相关

适用范围：大 N 小 T

基本思想：在广义矩估计的基础上增加了更多可用的工具变量，在 $t=3$ 处，$y_{i1}$ 可以作为所有滞后项的工具变量；在 $t=4$ 处，$y_{i1},y_{i2}$ 可以作为所有滞后项的工具变量；……
$$
\Delta y_{it} = a_1\Delta y_{i(t-1)} + a_2\Delta X_{it} + \Delta v_{it}\\\ \\X_{it} = [x_{it}, w_{it}]
$$
所有工具变量构成的矩阵如下：
$$
Z_i=\begin{bmatrix}&y_{i1}&0 &0 &0 &0 &0 &\dots &0 &\dots &0 &\Delta x_{i3}\\&0 &y_{i1} &y_{i2} &0 &0 &0 &\dots &0 &\dots &0 &\Delta x_{i4}\\&0 &0 &0 &y_{i1} &y_{i2} &y_{i3} &\dots &0 &\dots &0 &\Delta x_{i5}\\&\vdots &\vdots &\vdots &\vdots&\vdots&\vdots&\vdots&\vdots&\vdots&\vdots&\vdots \\&0 &0 &0 &0 &0 &0 &\dots &y_{i1} &\dots &y{i(T-2)} &\Delta x_{iT}\end{bmatrix}
$$
$Z_i$ 的行数为 $T-2$

$Z_i$ 的列数为 $\sum_{m=1}^{T-2}{m} + K$, K 为 X 的列数



设定工具变量的基本原则：

- 对内生变量的处理：与上述方法类似，即**滞后两阶以上的水平变量**均可作为差分方程的工具变量 (GMM type)
- 对先决变量的处理：**滞后一阶以上的水平变量**均可作为工具变量 (GMM type) 
- 对外生变量的处理：自己作为自己的工具变量 (Standard IV)



#### 一阶差分 GMM 估计量的基本设定

解释变量仅包含 $y_{it}$ 的一阶滞后项（默认设定），干扰项同方差，一阶段估计

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984 
est store ab_0
```

结果解释：

- 外生变量的工具变量等于外生变量的个数：L(0/1).x1  L(0/2).(x2 x3)  yr1980-yr1984 共 13 个

- 内生变量的工具变量：共 27个

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lag(3)
	/* 进一步设定 y_{i(t-2)} 和 y_{i(t-3)} 为解释变量 */
```



### 工具变量合理性检验

#### 过度识别检验*

原假设：工具变量的设定是合理的

```stata
estat sargan
```

当干扰项存在异方差时，Sargan 检验倾向于过度拒绝原假设，因此得到的**拒绝原假设的结论并不可信**

**采用两阶段估计，然后再执行 Sargan 检验较为稳妥**

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, twostep
estat sargan
```

当干扰项存在异方差时，在两阶段估计后执行 Sargan 检验往往倾向于过度接受原假设，通常而言，这很可能是我们的模型设定不当，或是工具变量的选择不合理



### 干扰项序列相关检验

一阶差分估计量要求**原始模型的干扰项**不存在序列相关，显然，差分后的干扰项**必然存在一阶序列相关**，因此，我们需要检验差分方程的残差是否存在**二阶（或更高阶）序列相关**即可

#### 二阶序列相关检验（默认）*

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, vce(robust)
estat abond
```

若存在二阶相关，则意味着选取的工具变量不合理



#### 高阶序列相关检验

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, vce(robust) artest(阶数)
estat abond
```



### 稳健型估计

得到考虑了**异方差**和**序列相关**后的标准误

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lag(2) robust
```

注意，此时无法使用 Sargan 统计量



### 联合检验

检验除常数项和时间虚拟变量外，其它变量整体上是否显著

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lag(2) robust
test  L.y L2.y  x1 L.x1  x2 L.x2 L2.x2  x3 L.x3 L2.x3
	/* 使用 Wald 检验联合显著性 */
```



### 两阶段估计考虑异方差问题

利用第一阶段估计得到的残差构造方差-协方差矩阵，进而重新估计模型

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lags(2) twostep
```

此时，Sargan 检验无法拒绝原假设



**重要建议**：

1. 采用两阶段估计给出的 Sargan 统计量进行**模型筛选**

2. 采用纠偏（bias-corrected, WC）后的稳健型 VCE，可以更好地进行**统计推断** 

   ```stata
   xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lags(2) twostep vec(robust)
   ```



### 先决变量的设定

先决变量：外来干扰不会影响当期变量的取值，但是会影响随后期变量的取值

**先决变量从滞后一期开始作为工具变量**（因为当期不受影响）

```stata
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1 x2)
	/* 假设 x1, x2 为先决变量 */
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1,lag(1,3)) pre(x2,lag(2,.))
```

注意：此时**逗号前就不能再出现这两个变量了**

解释：

- pre(x1,lag(1,3)) 中 **lag(1,3) 中的 1** 表示 L.x1 会作为解释变量

- pre(x1,lag(1,3)) 中 **lag(1,3) 中的 3** 表示 L2.x1 和 L3.x1 可以作为 L.x1 的工具变量，但 L4.x1 不可以
- pre(x2,lag(2,.)) 中 **lag(2,.) 中的 2** 表示 L1.x2, L2.x2 都会作为解释变量
- pre(x2,lag(2,.)) 中 **lag(2,.) 中的 .** 表示 L3.x2, L4.x2 ... 都可以作为 L2.x2 的工具变量



### 内生变量的设定

当期的干扰会影响内生变量当期的取值，同时也会影响后期的取值

**内生变量从滞后两期开始作为工具变量**

```stata
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep endog(x1,lag(1,.)) endog(x2,lag(2,.))
```



### 部分先决，部分内生

```stata
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1,lag(1,.)) endog(x2,lag(2,.))
```



注意：述设定都可附加 `vce(robust)` 选项以便获得稳健型标准误



### 工具变量过多导致的问题

过多的工具变量往往导致过度约束假设无法满足，估计结果的有效性降低，因为部分工具变量与内生或先决变量的相关性很弱

解决办法：限制最大的滞后阶数

```stata
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1,lag(1,3)) pre(x2,lag(2,3))
```



### 结果输出

```stata
reg y L.y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, robust
est store OLS
xtreg y L.y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, fe robust 
est store FE
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, robust
est store DAbond  

local model "OLS DAbond FE ab4_pre ab4_endog"
esttab `model', b(%6.3f) t(%6.2f)  mtitle(`model')   ///
				star(* 0.1 ** 0.05 *** 0.01)         ///
				scalar(arm2 sargan sar_df N N_g)     ///
				compress
```

**xtabond4 命令**

```stata
xtabond4 y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984
est store m_1
xtabond4 y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1,lag(1,.)) pre(x2,lag(2,.))
est store m_2 
local model "m_1 m_2"
esttab `model', b(%6.3f) t(%6.2f)  mtitle(`model')  ///
				star(* 0.1 ** 0.05 *** 0.01)         ///
				scalar(arm1 p_arm1 arm2 p_arm2 sargan sar_p sar_df N N_g)
* 或
local model "m_1 m_2"
esttab `model', b(%6.3f) t(%6.2f)  mtitle(`model')    ///
                star(* 0.1 ** 0.05 *** 0.01)          ///
                scalar(p_arm2 sargan sar_df sar_p N N_g)
```



### 3. 系统 GMM 估计量

适用范围：大 N 小 T

- 水平值：y  x

- 差分值：$\Delta y$  $\Delta x$  

- 水平方程：$y_{it} = b_1y_{i(t-1)}+ b_2x_{it}   + u_i + v_{it}$

  可用工具变量：$\Delta y_{i(t-2)}$ 可作为 $\Delta y_{i(t-1)}$ 的工具变量

- 差分方程：$\Delta y_{it} = b_1\Delta y_{i(t-1)} + b_2\Delta x_{it}+ \Delta v_{it}$ 

  可用工具变量：$y_{i(t-3)},y_{i(t-4)},\dots$ 可作为 $\Delta y_{i(t-1)}$ 的工具变量



#### 系统 GMM 估计量与差分 GMM 估计量的区别

1. 差分 GMM 估计量采用水平值的滞后项作为**差分变量的工具变量**，如 $y_{i(t-3)}$ 是 $\Delta y_{i(t-1)}$ 的工具变量
2. 系统 GMM 估计量进一步采用差分变量的滞后项作为**水平值的工具变量**，相当于进一步增加了可用的工具变量，且估计过程中同时使用水平方程和差分方程
3. **差分 GMM 的工具变量往往是弱工具变量，即 $Corr(X,Z)$ 过低**



### xtdpdsys 命令

```stata
local xx "x1 x2 x3 L(0/1).x4 L(0/1).x5 时间虚拟变量"
xtdpdsys y `xx', vce(robust) twostep
```

说明：

- 无需设定 L.y，xtdpdsys 默认设定被解释变量的滞后一期为解释变量，而且该变量被自动设定为内生变量
- 稳健型估计采用 vce(robust) 选项加以设定
- 该命令不会自动进行 AR(2) 序列相关检验和 Sargan 过度识别检验



#### 序列相关检验

```stata
estat abond
```



#### 过度识别检验

稳健型估计下无法获得 Sargan 统计量

```stata
* 过度识别检验的正确方法
local xx "x1 x2 x3 L(0/1).x4 L(0/1).x5 时间虚拟变量"
xtdpdsys y `xx', twostep       
estat sargan
```



#### 先决变量的设定

```stata
* 假设 x4 x5 为先决变量
local xx "x1 x2 x3 时间虚拟变量"
xtdpdsys tl `xx', pre(x4 x5,lag(1,.)) twostep  
est store dycs_2pre
estat abond
estat sargan
```



#### 内生变量的设定

```stata
* 假设 x4 x5 为内生变量
local xx "x1 x2 x3 时间虚拟变量"
xtdpdsys tl `xx', endog(x4 x5,lag(1,.)) twostep  
est store dycs_2pre
estat abond
estat sargan
```



### xtdpd 命令

即可估计一阶差分 GMM，又可估计系统 GMM，只是选项的设定相对复杂一些

#### 一阶差分估计量：所有解释变量外生

```stata
* 假设所有解释变量外生
xtdpd y L(1/2).y L(0/1).(x1 x2) x3, dgmmiv(y) div(L(0/1).(x1 x2) x3) 
	/* dmmiv()：内生变量 */
	/* div()：外生变量 */
est store r1

* 对比 xtabond 命令
xtabond y L(0/1).(x1 x2) x3, lag(2)
est store r2
esttab r1 r2
```

对比：

- xtdpd 需要设定 L(1/2).y 来标明解释变量中包含 $y_{i(t-1)}$ 和 $y_{i(t-2)}$ ，而 xtabond 通过选项 lag(2) 来进行该设定
- xtdpd 需要明确指定内生变量（选项 dgmmiv() ）以及外生变量（选项 div()），而 xtabond 命令无需这些设定，在不附加 endog() 选项的情况下，默认把 $y_{i(t-1)}$ 和 $y_{i(t-2)}$ 视为内生变量



####  一阶差分估计量：所有解释变量外生+稳健型标准误

```stata
xtdpd y L(1/2).y L(0/1).(x1 x2) x3, dgmmiv(y) div(L(0/1).(x1 x2) x3) vce(robust)
est store r1

* 对比 xtabond 命令
xtabond y L(0/1).(x1 x2) x3, lag(2) vce(robust)
est store r2
esttab r1 r2
```



#### 一阶差分估计量：所有解释变量内生+稳健型标准误

```stata
xtdpd L(0/1).(y x1 x2) x3 时间虚拟变量, dgmmiv(y x1 x2) div(x3 时间虚拟变量) vce(robust)
```

- 对于dgmmiv(x) 选项中的变量，L(2/.).x 均作为差分方程的工具变量
- 对于div(x) 选项中的变量，D.x 均作为差分方程的工具变量



#### 系统 GMM 估计量：所有解释变量外生

```stata
xtdpd y L(1/2).y L(0/1).(x1 x2) x3, dgmmiv(y) lgmmiv(n) div(L(0/1).(x1 x2) x3)
est store r1

xtdpdsys n L(0/1).(w ys) k, lag(2)
est store r2
esttab r1 r2  
```

对比：

- xtdpd 需要设定 L(1/2).y 来标明解释变量中包含 $y_{i(t-1)}$ 和 $y_{i(t-2)}$，而 xtdpdsys 通过选项 lag(2) 来进行该设定
- xtdpd 需要明确指定**差分方程的内生变量**（选项 dgmmiv() ），**水平方程的内生变量**（lgmmiv()）以及外生变量（选项 div()），而 xtdpdsys命令无需这些设定，在不附加 endog() 选项的情况下，默认把 $y_{i(t-1)}$ 和 $y_{i(t-2)}$ 视为内生变量



#### 系统 GMM 估计量：所有解释变量内生+两阶段估计

```stata
xtdpd L(0/1).(y x1 x2) 时间虚拟变量, dgmmiv(y x1 x2) lgmmiv(y x1 x2) div(时间虚拟变量) twostep
est store r1
estat abond
estat sargan
```



#### Sargan 检验拒绝原假设

有些情况下，Sargan 检验拒绝原假设有可能是干扰项 $e_{it}$ 不满足 i.i.d 假设

假设 $e_{it}\sim MA(1)$，即：
$$
y_{it} = a_0y_{i(t-1)} + a_1x_{it} + u_i + e_{it} + be_{i(t-1)}
$$
一阶差分后可得：
$$
\Delta y_{it} = a_0\Delta y_{i(t-1)} + a_1\Delta x_{it} +\Delta e_{it} + b\Delta e_{i(t-1)}
$$
由于 $\Delta e_{i(t-1)} = e_{i(t-1)} - e_{i(t-2)}$ ，所以**滞后三阶的变量是合理的工具变量**

```stata
xtdpd y L.y L(0/2).(x1 x2) x3 时间虚拟变量, div(L(0/1).(x1 x2) x3 时间虚拟变量) dgmmiv(y,lag(3 .)) hascons
	/* 注意此处的变化 */
estat sargan
```



### 4. xtlsdvc 命令（纠偏 LSDV 估计）

LSDV：虚拟变量最小二乘法（即，固定效应模型）

适用范围：**大 T 小 N**，当 N 较小时，采用 GMM 往往效果欠佳，因为 GMM 是基于大样本的，此时，bias-corrected LSDV estimator (LSDVC) 是个不错的选择，但该方法无法得到标准误，必须通过 Bootstrap 方法获得

模型：
$$
y_{it} = ay_{i(t-1)} + bx_{it} + u_i + e_{it}
$$
$x_{it}$ ：严格外生变量（这是该方法的一个重要假设，也是一个主要的限制）

$u_i$ ：个体效应

$e_{it}$ ：i.i.d 干扰项



initial() 参数：

- ah：一阶差分 IV 估计
- 一阶差分 GMM 估计
- bb：系统 GMM 估计

```stata
xtlsdvc y x1 x2 x3 时间虚拟变量, initial(ah)
est store r_AH82
xtlsdvc y x1 x2 x3 时间虚拟变量, initial(ab) 
est store r_AB91
xtlsdvc y x1 x2 x3 时间虚拟变量, initial(bb) 
est store r_BB98 

xtreg y L.y x1 x2 x3 时间虚拟变量, fe
est store r_FE

xtlsdvc y x1 x2 x3 时间虚拟变量, initial(ab) bias(3) vcov(10)
	/* bias()：精度，默认 1，最高 3 */
	/* vcov()：采用 Bootstrap 获得标准误，表示 Bootstrap 的次数 */
est store r_AB_bs

xtlsdvc n w k ys yr1980-yr1984, initial(ah) first lsdv
	/* 选项 first 呈现 IV 估计的结果 */
	/* 选项 lsdv 呈现 FE 的估计结果 */
	
* 对比结果
local mm "r_AH82 r_AB91 r_BB98 r_FE r_AB_bs"
esttab `mm', mtitle(`mm')
```

建议：

- ah：不建议适用

- ab：适用于一阶滞后项系数很小

- bb：适用于一阶滞后项系数接近 1



### 面板分析的一些结论

#### 1. 静态面板

- 当 $Corr(x,u_i)\ne0$ 时，OLS 和 RE 估计都是有偏的，此时，Hausman 检验倾向于得出负值



#### 2. 动态面板（大 N 小 T）

##### 一阶差分 GMM 估计量（AB91 估计量）的性质

- 在真实 DPG 设定下，AB91 估计量无偏
- 若 L2.y 或 L.x 不显著，则删除，否则会影响真实系数估计值
- 多数情况下，外生变量 X 的系数估计值很少受到影响
- 采用一阶段标准误进行统计推断比较合理

- 若**将外生变量设定为先决变量**，问题不大

   *     若**将外生变量设定为内生变量**，则会导致严重的下偏偏误，即**低估真实值**
   *     模型设定中加入多余的解释变量，并不影响序列相关 AR(2) 检验和 Sargan 检验（工具变量的合理性）结果



##### 系统 GMM 估计量（BB98 估计量）的性质

- 结论与一阶差分 GMM 估计量相似

- 随着被解释变量一阶滞后项系数值趋近于 1（超过 0.9），BB98 的确明显优于 AB91



#### 2. 动态面板（小 N 大 T）

比较 AB91, BB98, 与纠偏 LSDVC 的优劣：

- 当被解释变量一阶滞后项系数值等于 0.4 时，BB98 的表现通常较好
- 当被解释变量一阶滞后项系数值等于 0.9 时，纠偏 LSDVC 的优势比较明显，即使 $Corr(x,u_i)\ne0$ 该结论仍成立





## 面板单位根检验

优势：

- 克服传统单个时序单根检验的小样本偏误
- 利用面板从一定程度上控制不可观测的个体效应和截面相关性

局限：

- 由于面板中包含多个时序，各自的平稳性不同，因此在拒绝原假设的情况下，并不能得出所有序列均平稳的结论
- 理论上不断有新的成果出现，目前仍然存在诸多争议



### Multivariate ADF based test

适用范围：T > N，因为估计过程采用“似无相关模型 (SURE)”

原假设：面板中的所有截面对应的序列都是非平稳的，即 I(1) 过程（允许每个截面有不同的滞后阶数，而这些滞后项的系数之和在原假设下等于1）

注意：面板中任何一个序列是平稳的都可能导致原假设被拒绝，因此，拒绝原假设并不表明所有序列都是平稳的，只能保守地说，至少有一个序列是平稳的

```stata
madfuller y, lags(滞后阶数)
```



### Individual ADF based tests

#### 1. pescadf 命令

适用范围：T > N

原假设：面板中的所有截面对应的序列都是非平稳的，即 I(1) 过程

统计量：单个截面 DF 或 ADF 检验得到的 t 值的平均值

特点：考虑截面异质性和**截面相关**

```stata
pescadf y, lags(滞后阶数)
pescadf y, lags(滞后阶数) trend
	/* trend：包含时间趋势 */
pescadf y, lags(滞后阶数) trend trunoff
	/* trunoff: 对个别截面得到的统计量的离群值进行处理 */
```

> t-bar: 各个截面 ADF 检验得到的 t 值的平均值



#### 2. ipshin 命令*

适用范围：T > N

原假设：面板中的所有截面对应的序列都是非平稳的，即 I(1) 过程

统计量：对单个截面执行 ADF 检验后得到的 **t 值**的平均值

特点：考虑截面异质性和干扰项的**序列相关**问题

局限：要求面板是平衡的

```stata
ipshin y, lags(滞后阶数)
	/* 可以针对每个截面单独设计滞后阶数，如 lag(2 2 2 3 4 4 3 2) */
	/* nodemean：不删除每个截面的时间趋势效果 */
```



#### 3. xtfisher 命令

适用范围：T > N

**基本思想**：以个体单位根检验的 **p 值**为基础构造统计量（以 Fisher 检验为基础）

原假设：面板中的所有截面对应的序列都是非平稳的，即 I(1) 过程

优点：适用于非平衡面板

```stata
xtfisher y, lag(滞后阶数) trend
xtfisher y, lag(滞后阶数) pp
	/* 采用 PP 检验（默认是 ADF 检验） */
xtfisher y, lag(滞后阶数) display
	/* display：列出每个时间序列单独的检验结果 */
```



#### 4. levinlin 命令

**适用范围：大 N 小 T** （模型的估计采用 xtreg, fe 命令，因此允许个体效应）

原假设：面板中的所有截面对应的序列都是非平稳的，即 I(1) 过程

特点：考虑截面异质性和干扰项的**序列相关**问题

统计量：对单个截面执行 ADF 检验后得到的 **t 值**的平均值，并作相应调整，该统计量在原假设下服从正态分布

假设：所有序列均服从 AR(1)，且相关系数相同，但允许个体固定效应

**注：若原假设被拒绝，则认为所有序列均平稳，这是与其他几个检验方法不同之处**

```stata
levinlin y, lag(滞后阶数) trend
	/* 同样可单独列出每个截面的滞后阶数 */
```



#### 5. hadrilm 命令

**原假设：所有序列均平稳**

估计方法：LM，统计量在原假设下服从正态分布

特点：考虑截面异质性和干扰项的**序列相关**问题

```stata
hadrilm y
```

结果解释：

- Homo：假设每个截面的干扰项存在**同方差**
- Hetero：假设每个截面的干扰项存在**异方差**
- SerDep：假设每个截面存在**序列相关**





## 面板协整检验

### nharvey 命令：基于残差的检验

**基本思想**：检验残差序列构成的矩阵的 Rank (秩) 是否为 0（如果为 0 表明不存在协整关系），该矩阵的 Rank 即为协整向量的个数

**原假设**：每个截面内所有向量之间**不存在协整关系**，即协整向量的个数为 0

临界值的渐进分布特征决定于截面的个数 N 和预先假设的协整向量的个数，该检验允许存在序列相关

```stata
nharvey y
nharvey y, vlag(滞后阶数)
nharvey y, vlag(滞后阶数) trend
```



### xtwest 命令：基于误差修正模型的检验

**基本思想**：以误差修正模型为基础进行面板协整检验，若存在协整关系，则误差修正部分的系数应显著异于零

优点：基于残差的面板协整检验统计量隐含着一个重要的假设条件：长期误差修正系数（变量的水平值） = 短期动态调整系数（变量的差分值），称之为“同要素限制” (common factor restriction)，当这一假设无法满足时，以残差为基础的面板协整检验统计量的检定力会大幅降低，因此，以误差修正模型为基础进行协整检验可避免这一限制

特点：考虑截面异质性（长期误差修关系和短期动态关系）、截面内的序列相关和截面间的相关性

检验统计量：

- H0：不存在协整关系
- H1：面板整体上存在协整关系（2个）
- H1：至少存在一对协整关系

模型：
$$
\begin{matrix} 
\Delta y_{it} &= \underbrace{s_id_t}\\&时间趋势项\\
&+\underbrace{a_i(y_{i(t-1)}-b_ix_{i(t-1)})}\\&长期误差修正项\\
&+ \underbrace{a_{i1}\Delta y_{i(t-1)} + a_{i2}\Delta y_{i(t-2)}\dots+a_{ip}\Delta y_{i(t-p)}}\\&短期动态关系\\
&+\underbrace{b_{i0}\Delta x_{it}+ b_{i1}\Delta x_{i(t-1)}\dots+ b_{ip}\Delta x_{i(t-p)}}\\&短期动态关系\\
\ \\
&+e_{it}
\end{matrix}
\tag{1}
$$


含义：若存在误差修正机制，则 $a_i\lt0$ 且显著，此时也就表明 $y_{it}$ 和 $x_{it}$ 之间存在协整关系；否则，就表明二者之间不存在协整关系

把长期误差修正项的 $a_i$ 乘进去，整理得：
$$
\begin{matrix} 
\Delta y_{it} &= \underbrace{s_id_t}\\&时间趋势项\\
&+\underbrace{a_iy_{i(t-1)}+c_ix_{i(t-1)}}\\&长期误差修正项\\
&+ \underbrace{a_{i1}\Delta y_{i(t-1)} + a_{i2}\Delta y_{i(t-2)}\dots+a_{ip}\Delta y_{i(t-p)}}\\&短期动态关系\\
&+\underbrace{b_{i0}\Delta x_{it}+ b_{i1}\Delta x_{i(t-1)}\dots+ b_{ip}\Delta x_{i(t-p)}}\\&短期动态关系\\
\ \\
&+e_{it}
\end{matrix}
\tag{2}
$$
其中，$c_i=-a_ib_i$ 

#### 第一组统计量：

假设：$a_i \ne a_j $ ，**不同的截面有不同的 $a_i$，** 即各个截面的误差修正速度不同

构造方法：针对每个截面，采用 OLS 估计模型 (2)，得到 $a_1, a_2,\dots, a_N$ 的估计值和标准误（即 $y_{i(t-1)}$ 的系数）



##### Gt 统计量：不考虑序列相关

$$
Gt = \frac{1}{N}(\frac{a_1}{se_1} + \frac{a_2}{se_2}+\dots+\frac{a_N}{se_N})
$$

$se_i$ 是 OLS 估计得到的标准误

$Gt$ 可以看作对 t 值求平均



##### Ga 统计量：考虑序列相关（采用 Newey and West 方法计算标准误）

$$
Ga = \frac{1}{N}(T\frac{a_1}{SE_1} + T\frac{a_2}{SE_2}+\dots+T\frac{a_N}{SE_N})
$$

$SE_i$ ：采用 Newey and West 考虑序列相关后的标准误

$T$ ：每个截面内样本区间的长度



**H0**：不存在协整关系，在原假设成立的条件下，Gt 和 Ga 统计量均服从 N(0,1)

**H1**：至少存在一组协整关系



#### 第二组统计量：

假设：$a_i = a_j = a$ ，**各个截面有相同的 $a_i=a$，**即各个截面的误差修正速度相同

构造方法：利用第一阶段的 OLS 残差计算 $a$ 的估计值和标准误 $se_a$



##### Pt 统计量：不考虑序列相关

$$
Pt = \frac{a}{se_a}
$$



##### Pa 统计量：考虑序列相关

$$
Pa = Ta
$$

$T$ ：样本时间长度



**H0**：不存在协整关系，在原假设成立的条件下，Pt 和 Pa 统计量均服从 N(0,1)

**H1**：面板整体上存在协整关系



```stata
xtwest y x, westerlund constant trend lags(1 3) leads(0 3) lrwindow(3) noisily
	/* westerlund: 呈现 Westerlund(2007)文中的结果 */
	/* constant: 附加常数项 */
	/* trend: 附加时间趋势项 */
	/* lags(1 3): 相当于 lag(1/3) */
	/* leads(0 3): 相当于 leads(-3/3) */
	/* lrwindow(3): 采用半参数方法计算 a_i 的标准误，指定 Bartlett 核函数的窗口大小 */
	/* noisily: 显示所有截面的回归结果 (针对每个截面做误差修正模型) */
```

lags(1 3) ：加入短期动态关系中 y 的滞后项的结构 $\Delta y_{i(t-1)},\ \Delta y_{i(t-2)},\ \Delta y_{i(t-3)}$ 

lead(0 3)：加入短期动态关系中 x 的滞后项的结构 $\Delta x_{i(t+1)},\ \Delta x_{i(t+2)},\ \Delta x_{i(t+1)},\ \Delta x_{i(t)},\ \Delta x_{i(t-1)},\ \Delta x_{i(t-2)},\ \Delta x_{i(t-3)}$ 

```stata
xtwest y x, constant trend lags(1) leads(1) lrwindow(3) bootstrap(500)
	/* 采用 Bootstrap 方法 */
```

> 推荐用 Bootstrap 方法





## 面板误差修正模型：xtpmg 命令

适用于：大 N 大 T；考虑个体固定效应；非稳定序列

对于动态面板而言，两种类型的面板资料的大样本性质存在显著差异：

- **大 N 小 T** 面板重点关注**截面特征**，因此通常采用 **FE，RE 或 GMM**，只允许截距发生变化，而各个截面的斜率则是相同的
- **大 N 大 T** 面板则需同时关注**截面和时序特征**，因此斜率相等的假设可能并不合理
- **大 N 大 T** 面板由于每个截面内部的时序都较长，所以**稳定性**也是一个需要重点关注的问题



### 三种估计量：

#### MG 估计量

假设：各个截面的**长期和短期系数均不同**，即**完全的截面异质性**

估计方法：OLS 获得每个截面的估计系数



#### FE 估计量

假设：各个截面具有**相同的短期和长期系数**，但有不同的截距项（**个体效应**）

估计方法：FE (xtreg, fe cluster(id)) 考虑截面相关性



#### PMG 估计量

假设：各个截面的**长期系数都相等**，**误差修正速度和短期动态系数具有截面异质性**

估计方法：MLE



```stata
* 面板单位根检验
ipshin y, lags(3)
ipshin D.y, lags(3)
ipshin x1 , lags(3)
ipshin D.x1, lags(3)
ipshin x2 , lags(3)
ipshin D.x2, lags(3)
* 存在单位根过程，可以进行协整分析
```



### 建立误差修正模型

#### 1. 采用 PMG(Pooled Mean-Group) 估计量  (pmg 选项)

同时利用各个序列单独估计和整体混合估计的系数

假设：所有个体的长期弹性系数相等误差修正系数均相等，但短期系数则可随个体改变

```stata
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ec) pmg
	/* ec()：长期均衡关系，ec(ec) 是为了更改显示名称，为了好看 */
est store pmg
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ErrorC)  pmg
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2 ec(ec) full replace pmg
	/* replace：如果已经有 ec 变量，则自动替换成新的 */
	/* full：把每个截面的结果都显示出来 */
```

结果解释：

ec：长期均衡关系

SR 中的 ec：长期调整速度的系数

SR 中其他项：短期调整关系



#### 2. 采用 MG(Mean-Group) 估计量  (mg 选项)

分别估计 N 个时间序列，取它们的平均值

```stata
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ec) replace mg
est store mg
```



#### 3. 采用固定效应动态面板估计量 (dfe 选项)

假设：长期误差修正系数和短期系数均为常数，不随个体改变

```stata
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ec) replace dfe
est store dfe

xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ec) replace dfe cluster(截面变量)
	/* 允许截面间存在相关性 */
est store dfe_cluster 
```



#### 结果对比

```stata
esttab pmg mg dfe dfe_cluster, mtitle(pmg mg dfe dfe_cluster)
```





## 模型的筛选

pmg, mg 和 dfe 三者对模型参数的限制有所不同：

- mg：长短期系数均随个体变动
- pmg：长期不变，短期可变
- dfe：长短期均不可变

> 越往下，约束越多，变量越少
>
> 如果约束多的约束条件是正确的，则它比较有效，因为使用了较少的参数；否则将不一致
>
> 下面和上面的关系可以类比为：下面的类比为随机效应模型，上面的类比为固定效应模型

三者之间，两两比较

```stata
hausman pmg mg
hausman dfe pmg
hausman dfe mg
```

> 若卡方值小于 0，倾向于拒绝原假设，即认为约束不成立