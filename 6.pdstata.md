# 面板数据纯 Stata 操作

## 面板数据的设定

```stata
xtset 面板变量名 时间变量名
	# xtset：数据为面板数据
	# 面板(个体)变量：取值须为整数且不重复，相当于将样本中每位个体进行编号
```



## 字符串转整数

```stata
encode country, gen(新变量名)
	# gen(新变量名)：将新生成的数字型变量记为新变量名
```



## 显示面板数据统计特性

```stata
xtdes
	# 显示面板数据的结构，是否为平衡面板
xtsum
	# 显示组内、组间与整体的统计指标)
xtline 变量名
	# 对每位个体分别显示该变量的时间序列图
	, overlay : 将所有个体的时间序列图叠放在一起
```



## tsset 显示有重复时间

```stata
uplicates report 截面变量 时间序列变量
duplicates example 截面变量 时间序列变量
list if 截面变量 == 重复的截面值
duplicates drop 截面变量 时间序列变量, force
tsset 截面变量 时间序列变量
```



## 非平衡面板变为平衡面板

```stata
tsset 截面变量 时间序列变量
xtdes
	/* 观察时间序列交集的部分是从 xx 到 yy */
sum
	/* 观察截面数据有无缺失值 */
xtbalance , rang(xx yy) miss(_all)
	/* 一次性处理缺失值和非平衡 */
xtdes
sum
```



## 得到时间连续的样本

时间连续未必是平衡的面板数据

```stata
cap erase grun1.dta
onespell x1 x2 x3 x4, saving(数据文件名) replace
	# 保留尽可能长的连续时间
	# replace：若数据文件已存在同名的，则替代
use 数据文件名.dta, clear
xtdes
```



## 得到连续的公司编号

在使用循环命令(如forvalues)时，需要公司编号是连续的，此时可采用 egen 命令提供的 group() 函数

```stata
tsset 截面变量 时间序列变量
egen code_new = group(截面变量)
tsset code_new 时间序列变量
xtdes
```

采用同样的方法，可以针对任何类别变量生成连续编号



## 长条形数据与扁平型数据的转换

| id   | c1992 | c1993 | c1994 | c1995 | x1   | x2   |
| ---- | ----- | ----- | ----- | ----- | ---- | ---- |
| 1    | 84    | 38    | 87    | 78    | 55   | 75   |
| 2    | 6     | 8     | 8     | 4     | 6    | 4    |
| 3    | 66    | 56    | 43    | 75    | 35   | 64   |

```stata
reshape long c, i(id) j(year)
tsset id year
list in 1/20, sep(4)
```



## 面板数据绘图

```stata
xtline y
	/* 纵坐标是 y，横坐标是时间，根据不同的公司绘制多个子图 */

xtline y, overlay
	/* 绘制在一张图内 */
```

适合大 T 小 N



## 统计类别变量

```stata
xtab age
	/* 按截面个数统计 */

tabulate age
	/* 按样本数统计 */
```



## 统计个体的数目

```stata
xtcount if 变量 > 值
	/* 统计符合条件的截面的数量 */
```





## 混合回归

```stata
reg y x1 x2 x3, vce(cluster 面板个体的变量名)
	# vce(cluster id)：以变量 id 作为聚类变量来计算聚类稳健的标准误
```





## 固定效应模型

### 1. FE 法

```stata
xtreg y x1 x2 x3, fe r
    # xtreg：对面板数据进行回归
    # fe：fixed effects(固定效应估计量)
    # 默认为 “re” 表示 “random effects”(随机效应估计量)
    # r：使用聚类稳健标准误；如使用选择项 “vce(cluster 面板个体的变量名)” 也能达到相同效果
```



#### 获得调整后的 $R^2$ $adj-R^2$

```stata
areg y x1 x2, a(截面变量)
```



#### 获得截距项

**方法一**：加入 (N-1) 个虚拟变量，采用 OLS 估计

可以同时获得每个截距项的标准误、t 值和 p 值，适用于 大 T 小 N 型数据

```stata
tab 截面变量, gen(dum)
reg y x1 x2 dum*, nocons
	# 可选择添加 robust
```

**方法二**：xi 前缀

```stata
xi: reg y x1 x2 i.id
xi: reg y x1 x2 i.id, robust
```

**方法三**：predict 命令

```stata
qui xtreg y x1 x2, fe
predict a, u
	# u：得到截距项
replace a = _b[_cons] + a
duplicates example 截面变量 a
order 截面变量 时间序列变量 a
```

**方法四**：areg 命令

```stata
areg y x1 x2, absorb(截面变脸)         
predict ai, d
	# 得到截距
gen a_i = ai + _b[_cons]
order 截面变量 时间序列变量 a_i ai
```

**方法五**：fese 命令

```stata
fese y x1 x2, s(o) oonly
```



#### 获得拟合值和残差

```stata
xtreg y x1 x2 x3, fe
predict y_hat
	# 默认得到拟合值，即 xb
predict a, u
	# 得到个体效应，即每个截面的截距项
predict res, e
	# 得到残差
predict cres, ue
	# 得到残差和个体效应的和
```



### 2. LSDV 法

```stata
reg y x1 x2 x3 i.面板个体的变量名, vce(cluster 面板个体的变量名)
	# i.面板个体的变量名：根据面板个体变量而生成的虚拟变量
```



### 3. 一阶差分法（FD）

```stata
xtserial y x1 x2 x3, output
	# xtserial, output：对组内自相关进行检验时，可附带提供一阶差分法的估计结果
```





## 随机效应模型

### 1. RE 法

```stata
xtreg y x1 x2 x3, re r
	# re：“random effects” (随机效应估计量)，为默认选项(可省略)
	# theta：显示用于进行广义离差变换的 θ 值
	# r：使用聚类稳健标准误；如使用选择项 “vce(cluster 面板个体的变量名)” 也能达到相同效果
```



### 2. MLE 估计

```stata
xtreg y x1 x2 x3, mle
```





## 时间效应

### 双向固定效应（加入时间虚拟变量）

```stata
qui tab 时间变量, gen(yr)
drop yr1
	# 避免多重共线性
xtreg y x1 x2 x3 x4 yr*, fe
```



### 随机效应模型中的时间效应

```stata
xtreg y x1 x2 x3 x4 yr*, re
```



### 时间效应显著性检验

#### Wald 检验

```stata
xtreg y x1 x2 x3 x4 yr*, fe
test yr2 = yr3 = yr4 = yr5 = yr6 = yr7
test yr2 = yr3 = yr4 = yr5 = yr6 = yr7 = 0
```

若 p 值很小，表明显著不为 0，则说明存在时间效应



#### LR 检验

```stata
xtreg y x1 x2 x3 x4, fe
est store fe
xtreg y x1 x2 x3 x4 yr*, fe
est store fe_dumt
lrtest fe fe_dumt
```

若 p 值很小，则说明存在时间效应





## 模型的筛选

### 1. 固定效应模型还是 Pooled OLS

#### 方法 1：Wald 检验*

```stata
xtreg y x1 x2 x3 x4, fe
```

根据最后一行的 F test that all u_i=0 的 p 值，如果 p 值很小，则认为存在固定效应



#### 方法 2：LR 检验

```stata
qui tab 截面变量, gen(dum)
reg y x1 x2 x3 x4
est store m_ols
reg y x1 x2 x3 x4 dum*, nocons  
est store m_fe
lrtest m_ols m_fe
```

若 p 值很小，则认为存在固定效应



### 2. 随机效应模型还是 Pooled OLS

#### 方法 1：B-P 检验

```stata
xtreg y x1 x2 x3 x4, re
xttest0
```

若 p 值很小，则说明存在随机效应



#### 方法 2：LR test（似然比检验）

```stata
xtreg y x1 x2 x3 x4, re mle
```



### 3. 固定效应模型还是随机效应模型

#### 方法 1：Hausman 检验

原假设：固定效应与随机效应不存在差异

```stata
xtreg y x1 x2 x3 x4, fe
est store fe
xtreg y x1 x2 x3 x4, re
est store re
hausman fe re
	# constant：在比较系数估计值时包括常数项 (默认不含常数项)
    # sigmamore：统一使用更有效率的那个估计量(即随机效应估计量)的方差估计
    # 若 p 值等于 0，则拒绝H0“ui与解释变量不相关”，即应该使用固定效应模型
```

p 值越小，越认为存在差异，应采用固定效应模型



**Hausman 检验值为负**：通常是因为随机模型的基本假设 $Corr(x,u_i)=0$ 无法得到满足

```stata
hausman m_fe m_re, sigmaless
	* 检验过程中两个模型都采用固定效应模型的方差-协方差矩阵
hausman m_fe m_re, sigmamore
	* 检验过程中两个模型都采用随机效应模型的方差-协方差矩阵
```

p 值越小，越应采用固定效应模型



**如果 Hausman 检验拒绝随机效应模型**：

1. 使用固定效应模型
2. 使用工具变量法（IV）



#### 方法 2：过度识别检验

**基本思想**：如果固定效应和随机效应都是一致的，那么系数估计值应当不存在显著差异

```stata
ssc install xtoverid
	# overid：overidentification test (过度识别检验)
ssc install ivreg2
	# 安装依赖包
	
xtreg y x1 x2 x3 x4, re
	# 先执行稳健标准误命令
xtoverid
```

p 值越小，越应采用固定效应模型





## 组间异方差检验

### 固定效应模型

```stata
xtreg y x1 x2 x3 x4, fe
xttest3
```

p 值越小，越拒绝无异方差的原假设



### 随机效应模型

随机效应模型本身已经较大程度的考虑了异方差问题，主要体现在 $\sigma_u^2$ 上





## 序列相关检验

### 固定效应模型

#### 1. Wooldridge

若无序列相关，则一阶差分后残差相关系数应为 -0.5

```stata
xtserial y x1 x2 x3 x4
```

原假设：无一阶自相关



#### 2. Arellano-Bond 检验

```stata
reg y x1 x2 x3, cluster(截面变量)
abar
	/* 不考虑个体特征，只考虑截面相关性 */
	
reg y x1 x2 x3 dum*, nocons 
abar
	/* 不考虑截面相关性，只考虑个体特征 */
```



### 随机效应模型

```stata
xtreg y x1 x2 x3, re
xttest1
	/*提供多个统计检验量*/
```





## 截面相关检验

#### 1. xttest2 命令

原假设：所有截面残差的相关系数都相等

```stata
xtreg y x1 x2 x3, fe
xttest2
```

由于检验过程中执行了 SUE 估计，所以要求 T>N



### 2. xtcsd 命令

```stata
xtreg y x1 x2 x3, fe
xtcsd, pesaran     
	/* Pesaran(2004) */
xtcsd, friedman    
	/* Friedman(1937) */
xtcsd, frees       
	/* Frees(1995) */
xtcsd, pesaran show
	/* show：显示截面之间的相关系数 */
```

截面较多时，该方法耗时较长





## 异方差的处理

### 1. 异方差稳健型估计

```stata
xtreg y x1 x2 x3 x4, fe robust  
est store fe_rb
```



### 2. 采用Bootstrap标准误

```stata
xtreg y x1 x2 x3 x4, fe vce(bootstrap, reps(重复次数)) 
est store fe_bs
```





## 序列相关的处理

### 一阶自相关

#### 1. xtregar 命令

```stata
xtregar y x1 x2 x3 x4, fe 
est store fe_ar1

xtregar y x1 x2 x3 x4, fe lbi 
	/* lbi：提供 Baltagi-Wu LBI test 统计量 */
	
xtregar y x1 x2 x3 x4, re    
est store re_ar1 
```

这里的 Durbin-Watson 统计量具有较为复杂的分布，不同于时间序列中的 D-W 统计量，其临界值见 Bhargava et al. (1982, The Review of Economic Studies 49:553-549)

Baltagi-Wu LBI 统计量基本上没有太大的参考价值，因为他们并未提供临界值表，而该统计量的分布又相当复杂



#### 2. 两阶段估计

通过第一阶段拟合的一阶自相关系数，代回原模型中作为初始值再次做回归

```stata
xtregar y x1 x2 x3 x4, fe twostep
est store fe_ar1_two
```



#### 选择一阶相关系数的计算方法

```stata
xtregar y x1 x2 x3 x4, fe rhotype(tscorr)
	/* 指定一阶自相关系数的估计方法为：残差的当期值和滞后一阶的相关系数（协方差/标准差的乘积） */ 
est store fe_ar1_tscorr
```



#### 结果对比

```stata
xtreg y x1 x2 x3 x4, fe

est store fe
local models "fe fe_ar1 re_ar1 fe_ar1_two fe_ar1_tscorr"
esttab `models', b(%6.3f) se(%6.3f) mtitle(`models') r2 sca(r2_w corr)
```

注意：

- 面板数据分析中，除非 T 很大，通常没有必要列出 D-W 值
- 虽然能按照上述方法列出 D-W 值，但一定要给出临界值，否则没有参考意义，因为其分布与传统的 D-W 迥异



### 高阶自相关

高阶自相关不是很常见，主要存在于 N 很少，T 很长的面板数据中

#### 1. newey2 命令

```stata
newey2 y x1 x2 x3 x4, lag(滞后阶数)  

neweyvif 
	/* 得到膨胀因子(VIF) */
```

如果每一个解释变量的膨胀因子都小于 10，通常认为不存在多重共线性问题



#### 利用 newey2 命令得到膨胀因子 (VIF)

```stata
newey2 y x1 x2 x3 x4, lag(0)
	/* 纯粹为了利用 newey2 命令得到膨胀因子 */
neweyvif
```



#### 2. xtivreg2 命令

同时考虑异方差和序列相关

```stata
xtivreg2 y x1 x2 x3 x4, fe bw(2) robust small
	/*  fw()：参数只要大于 0 就行 */
```





## 截面相关（组间相关）的处理

### cluster 选项

```stata
xtreg y x1 x2 x3 x4, fe cluster(截面变量)

xtreg y x1 x2 x3 x4, re cluster(截面变量)
```





## 综合处理

### xtscc 命令

当异方差、序列相关以及截面相关性质未知时，推荐使用该方法

xtscc 相当于 White / Newey 估计扩展到面板的情形

```stata
xtscc y x1 x2 x3 x4, fe 
	/* 同时考虑异方差和截面相关 */

xtscc y x1 x2 x3 x4, fe lag(1)
	/* 同时考虑异方差、序列相关和截面相关 */
```





## 多方程模型

### 1. xtgls 命令

```stata
xtgls y x1 x2 x3 x4, panels(iid)   
	/* iid, 异方差、序列相关和截面相关都不考虑，等同于 Pooled OLS */
est store g_0
reg y x1 x2 x3 x4
	/* 等同于第一条命令 */
est store g_ols

xtgls y x1 x2 x3 x4, panel(het)
	/* 截面异方差 */
est store g_phet

xtgls y x1 x2 x3 x4, corr(ar1)     
	/* 所有截面具有相同的自相关系数 */
est store g_par1

xtgls y x1 x2 x3 x4, corr(psar1)   
	/* 每个截面有自己的自相关系数 */
est store g_psar1

xtgls y x1 x2 x3 x4, panel(corr)   
	/* 截面间相关且异方差 */
est store g_pcorr

xtgls y x1 x2 x3 x4, p(c) corr(ar1)
	/* 同时考虑异方差、序列相关和截面相关 */
est store g_all
```



#### 异方差的检验

```stata
xtgls y x1 x2 x3 x4, panel(het)    
	/* 截面异方差 */
xttest3
```



#### 序列相关的检验

```stata
xtserial y x1 x2 x3 x4
```



#### 截面相关的检验

```stata
xtgls y x1 x2 x3 x4, panel(het)
xttest2
```



### 2. xtpcse 命令

与 xtgls 的区别：xtgls 采用 GLS 进行估计，而 xtpsce 采用 OLS 进行估计

```stata
xtpcse  y x1 x2 x3 x4
	/* OLS 估计，同时调整异方差和截面相关后计算标准误 */
est store pcse_full

xtpcse y x1 x2 x3 x4, correlation(ar1)
	/* Prais_Winsten 估计，假设有共同的自相关系数 */
est store pcse_ar1

xtpcse y x1 x2 x3 x4, correlation(ar1) rhotype(tscorr)
	/* 指定自相关系数的计算方法 */
est store pcse_tscorr   

xtpcse y x1 x2 x3 x4, correlation(ar1) hetonly
	/* 只考虑截面异方差，不考虑截面相关 */
est store pcse_ar1      
```

当 N 较大时，采用该方法会非常费时，因为方差协方差矩阵是采用 OLS 估计的残差计算的





## 内生性问题 1. $Corr(x_{it},e_{it}) \ne 0$

一般情形下的内生性问题，Fe 和 Re 估计量都是非一致的

```stata
xtivreg y x1 x2 x3 (x4 = x5 x6 L.x4), fe
```



### 内生性的检验

#### 1. Davidson-MacKinnon 检验*

H0：OLS 和 xtivreg 都是一致的，即内生性问题对 OLS 的估计结果影响不大

```stata
xtivreg y x1 x2 x3 (x4 = x5 x6), fe
est store fe_iv
dmexogxt
```



#### 2. Hausman-Wu 检验

```stata
xtreg y x1 x2 x3 x4 x5 x6, fe
est store fe

hausman fe fe_iv
hausman fe fe_iv, sigmamore
hausman fe fe_iv, sigmaless
```

Hausman 检验的主要局限在于可能产生小于零的卡方值，这通常是拒绝原假设的表现，即认为存在内生性问题



### 工具变量的合理性检验

#### 过度识别检验

**原假设**：选用的工具变量是合理的

```stata
xtivreg y x1 x2 x3 (x4 = x5 x6), fe
xtoverid
```



#### 识别不足检验

**原假设**：存在识别不足问题，即工具变量与内生性变量不相关

```stata
xtivreg2 y x1 x2 x3 (x4 = x5 x6), fe
```

Anderson canon. corr. LR statistic (underidentification test) :

> 即使不存在识别不足问题，但仍然可能存在弱工具变量问题



#### 弱工具变量检验

**原假设**：工具变量与内生变量有较强的相关性

```stata
xtivreg2 y x1 x2 x3 (x4 = x5 x6), fe
```

Cragg-Donald F statistic (weak identification test) ：



#### 工具变量的选择

滞后项经常被用作工具变量



### 存在异方差的 IV 估计

#### 1. Bootstrap 法

```stata
xtivreg y x1 x2 x3 (x4 = x5 x6 L.x4), fe vce(bootstrap, rep(500))
	/* 使用 Bootstrap 获得标准误 */
```



#### 2. 异方差稳健型估计

```stata
xtivreg2 y x1 x2 x3 (x4 = x5 x6 L.x4), fe robust
	/* 异方差稳健型估计 */
```





### 存在序列相关的 IV 估计

```stata
gen Lag1_x4 = L.x4
gen Lag2_x4 = L2.x4
newey2 y x1 x2 (x3 = Lag1_x4 Lag2_x4) x5, lag(2) 
```





## 内生性问题 2. $Corr(x_{it},u_{i}) \ne 0$

### 处理方法

#### Hausman-Taylor Estimation

```stata
xtsum x21 x22 x23 x24 x25
	/* 随时间改变的个体随机效应 */
	
correlate z11 z12 x11 x12 x13 x14 z21
	/* 用严格外生变量作为不随时间改变的内生变量（z21）的工具变量 */

xthtaylor y x11 x12 x13 x14 x21 x22 x23 x24 x25 z11 z12 z21, endog(x21 x22 x23 x24 x25 z21)
	/* endog：指定内生变量，其他变量作为内生变量的工具变量 */
est store re_htaylor
```

结果解释：

1. 各种类型的变量分开列示（TV：随时间改变的变量；TI：不随时间改变的变量）
2. rho = 0.97  表明 $u_i$ 在残差的波动中占有很大的比例

**注意**：查看相关性很重要 `correlate`



### 检验

```stata
xthtaylor y x11 x12 x13 x14 x21 x22 x23 x24 x25 z11 z12 z21, endog(x21 x22 x23 x24 x25 z21)
xtoverid  
	/* 检验工具变量的合理性 */
xtoverid, robust
	/* 使用稳健型标准误，检验工具变量的合理性 */
xtoverid, cluster(id)
	/* 考虑截面相关，检验工具变量的合理性 */
```





## 面板随机系数模型

### 估计似无相关（SUE）模型

事实上是单个截面的 OLS 估计，只是考虑了截面相关性

```stata
reshape wide y x1 x2, i(时间序列变量) j(截面变量)
	/* 转换为列名 y1 x11 x21 y2 x12 x22 y3 x13 x23... 的形式*/
sureg (y1 x11 x21) (y2 x12 x22) (y3 x13 x23) (y4 x14 x24) (y5 x15 x25)
	/* 所有截面单独做 OLS 回归得到系数，方差-协方差矩阵用所有截面的方差-协方差联合起来得到 */
```

若结果显示每个截面的系数并不完全相同，则考虑用随机系数模型



### 估计随机系数模型

```stata
use xxx.dta, clear
xtrc y x1 x2, i(截面变量)
est store xt_rc
```

结果汇报的 $b$ 是平均的系数

```stata
xtrc y x1 x2, i(截面变量) betas
	/* betas：汇报每个截面的系数值 */
```





## 面板随机边界分析

### 面板模型 1：无效率成分不随时间改变

```stata
xtfrontier y x1 x2, ti
	/* ti：不随时间改变 */
est store xtsfa_ti
```

结果分析：

- sigma_u2 = $\sigma_u^2 $

- sigma_v2 = $\sigma_v^2$
- sigma2  = $\sigma_u^2 + \sigma_v^2$
- gamma = $\sigma_u^2 / (\sigma_u^2+\sigma_v^2)$   即无效率的波动占总波动的比例
- /ilgtgamma = $1/logit(\gamma)$ ， $logit(\gamma) = \gamma/(1-\gamma)$
- /lnsigma2, /ilgtgamma 之所以做转换，都是为了方便求解



### 面板模型 2：无效率成分随时间改变

```stata
xtfrontier y x1 x2, tvd
est store xtsfa_tvd
```

结果分析：如果 $\eta$ 的估计值非常接近 0，可以施加约束条件

```stata
constraint define 1 [eta]_cons = 0

xtfrontier y x1 x2, tvd constraints(1)
est store xtsfa_tvd_eta0
```



### 技术效率的估计

```stata
xtfrontier y x1 x2, ti
predict yhat    
predict eff_te, te   /* 一般应用此项，因为 eff_te 介于 0 和 1 之间 */
predict eff_u , u
predict eff_m , m
sum eff_*

gen eff = 1 - eff_te
histogram eff_te
histogram eff
```





## 动态面板内生性的处理

### 1. 一阶差分 IV 估计量

```stata
xtivreg y L2.y  x1 L1.x1  x2 L1.x2 L2.x2  x3 L1.x3 L2.x3  时间虚拟变量 (L.y = L3.y), fd
```

可以简写为：

```stata
xtivreg y L2.y L(0/1).x1 L(0/2).(x2 x3) 时间虚拟变量 (L.y = L3.y), fd
```



### 2. 一阶差分 GMM 估计量

#### 一阶差分 GMM 估计量的基本设定

解释变量仅包含 $y_{it}$ 的一阶滞后项（默认设定），干扰项同方差，一阶段估计

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984 
est store ab_0
```

结果解释：

- 外生变量的工具变量等于外生变量的个数：L(0/1).x1  L(0/2).(x2 x3)  yr1980-yr1984 共 13 个

- 内生变量的工具变量：共 27个

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lag(3)
	/* 进一步设定 y_{i(t-2)} 和 y_{i(t-3)} 为解释变量 */
```



### 工具变量合理性检验

#### 过度识别检验*

原假设：工具变量的设定是合理的

```stata
estat sargan
```

当干扰项存在异方差时，Sargan 检验倾向于过度拒绝原假设，因此得到的**拒绝原假设的结论并不可信**

**采用两阶段估计，然后再执行 Sargan 检验较为稳妥**

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, twostep
estat sargan
```

当干扰项存在异方差时，在两阶段估计后执行 Sargan 检验往往倾向于过度接受原假设，通常而言，这很可能是我们的模型设定不当，或是工具变量的选择不合理



### 干扰项序列相关检验

#### 二阶序列相关检验（默认）*

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, vce(robust)
estat abond
```

若存在二阶相关，则意味着选取的工具变量不合理



#### 高阶序列相关检验

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, vce(robust) artest(阶数)
estat abond
```



### 稳健型估计

得到考虑了**异方差**和**序列相关**后的标准误

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lag(2) robust
```

注意，此时无法使用 Sargan 统计量



### 联合检验

检验除常数项和时间虚拟变量外，其它变量整体上是否显著

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lag(2) robust
test  L.y L2.y  x1 L.x1  x2 L.x2 L2.x2  x3 L.x3 L2.x3
	/* 使用 Wald 检验联合显著性 */
```



### 两阶段估计考虑异方差问题

利用第一阶段估计得到的残差构造方差-协方差矩阵，进而重新估计模型

```stata
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lags(2) twostep
```

此时，Sargan 检验无法拒绝原假设

**重要建议**：

1. 采用两阶段估计给出的 Sargan 统计量进行**模型筛选**

2. 采用纠偏（bias-corrected, WC）后的稳健型 VCE，可以更好地进行**统计推断** 

   ```stata
   xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, lags(2) twostep vec(robust)
   ```



### 先决变量的设定

先决变量：外来干扰不会影响当期变量的取值，但是会影响随后期变量的取值

**先决变量从滞后一期开始作为工具变量**（因为当期不受影响）

```stata
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1 x2)
	/* 假设 x1, x2 为先决变量 */
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1,lag(1,3)) pre(x2,lag(2,.))
```

注意：此时**逗号前就不能再出现这两个变量了**

解释：

- pre(x1,lag(1,3)) 中 **lag(1,3) 中的 1** 表示 L.x1 会作为解释变量

- pre(x1,lag(1,3)) 中 **lag(1,3) 中的 3** 表示 L2.x1 和 L3.x1 可以作为 L.x1 的工具变量，但 L4.x1 不可以
- pre(x2,lag(2,.)) 中 **lag(2,.) 中的 2** 表示 L1.x2, L2.x2 都会作为解释变量
- pre(x2,lag(2,.)) 中 **lag(2,.) 中的 .** 表示 L3.x2, L4.x2 ... 都可以作为 L2.x2 的工具变量



### 内生变量的设定

当期的干扰会影响内生变量当期的取值，同时也会影响后期的取值

**内生变量从滞后两期开始作为工具变量**

```stata
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep endog(x1,lag(1,.)) endog(x2,lag(2,.))
```



### 部分先决，部分内生

```stata
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1,lag(1,.)) endog(x2,lag(2,.))
```

注意：述设定都可附加 `vce(robust)` 选项以便获得稳健型标准误



### 工具变量过多导致的问题

过多的工具变量往往导致过度约束假设无法满足，估计结果的有效性降低，因为部分工具变量与内生或先决变量的相关性很弱

解决办法：限制最大的滞后阶数

```stata
xtabond y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1,lag(1,3)) pre(x2,lag(2,3))
```



### 结果输出

```stata
reg y L.y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, robust
est store OLS
xtreg y L.y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, fe robust 
est store FE
xtabond y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984, robust
est store DAbond  

local model "OLS DAbond FE ab4_pre ab4_endog"
esttab `model', b(%6.3f) t(%6.2f)  mtitle(`model')   ///
				star(* 0.1 ** 0.05 *** 0.01)         ///
				scalar(arm2 sargan sar_df N N_g)     ///
				compress
```

**xtabond4 命令**

```stata
xtabond4 y L(0/1).x1 L(0/2).(x2 x3) yr1980-yr1984
est store m_1
xtabond4 y L(0/1).x3 yr1980-yr1984, lags(2) twostep pre(x1,lag(1,.)) pre(x2,lag(2,.))
est store m_2 
local model "m_1 m_2"
esttab `model', b(%6.3f) t(%6.2f)  mtitle(`model')  ///
				star(* 0.1 ** 0.05 *** 0.01)         ///
				scalar(arm1 p_arm1 arm2 p_arm2 sargan sar_p sar_df N N_g)
* 或
local model "m_1 m_2"
esttab `model', b(%6.3f) t(%6.2f)  mtitle(`model')    ///
                star(* 0.1 ** 0.05 *** 0.01)          ///
                scalar(p_arm2 sargan sar_df sar_p N N_g)
```



### 3. 系统 GMM 估计量

#### 1. xtdpdsys 命令

```stata
local xx "x1 x2 x3 L(0/1).x4 L(0/1).x5 时间虚拟变量"
xtdpdsys y `xx', vce(robust) twostep
```

说明：

- 无需设定 L.y，xtdpdsys 默认设定被解释变量的滞后一期为解释变量，而且该变量被自动设定为内生变量
- 稳健型估计采用 vce(robust) 选项加以设定
- 该命令不会自动进行 AR(2) 序列相关检验和 Sargan 过度识别检验



##### 序列相关检验

```stata
estat abond
```



##### 过度识别检验

稳健型估计下无法获得 Sargan 统计量

```stata
* 过度识别检验的正确方法
local xx "x1 x2 x3 L(0/1).x4 L(0/1).x5 时间虚拟变量"
xtdpdsys y `xx', twostep       
estat sargan
```



##### 先决变量的设定

```stata
* 假设 x4 x5 为先决变量
local xx "x1 x2 x3 时间虚拟变量"
xtdpdsys tl `xx', pre(x4 x5,lag(1,.)) twostep  
est store dycs_2pre
estat abond
estat sargan
```



##### 内生变量的设定

```stata
* 假设 x4 x5 为内生变量
local xx "x1 x2 x3 时间虚拟变量"
xtdpdsys tl `xx', endog(x4 x5,lag(1,.)) twostep  
est store dycs_2pre
estat abond
estat sargan
```



#### 2. xtdpd 命令

即可估计一阶差分 GMM，又可估计系统 GMM，只是选项的设定相对复杂一些

##### 一阶差分估计量：所有解释变量外生

```stata
* 假设所有解释变量外生
xtdpd y L(1/2).y L(0/1).(x1 x2) x3, dgmmiv(y) div(L(0/1).(x1 x2) x3) 
	/* dmmiv()：内生变量 */
	/* div()：外生变量 */
est store r1

* 对比 xtabond 命令
xtabond y L(0/1).(x1 x2) x3, lag(2)
est store r2
esttab r1 r2
```

对比：

- xtdpd 需要设定 L(1/2).y 来标明解释变量中包含 $y_{i(t-1)}$ 和 $y_{i(t-2)}$ ，而 xtabond 通过选项 lag(2) 来进行该设定
- xtdpd 需要明确指定内生变量（选项 dgmmiv() ）以及外生变量（选项 div()），而 xtabond 命令无需这些设定，在不附加 endog() 选项的情况下，默认把 $y_{i(t-1)}$ 和 $y_{i(t-2)}$ 视为内生变量



#####  一阶差分估计量：所有解释变量外生+稳健型标准误

```stata
xtdpd y L(1/2).y L(0/1).(x1 x2) x3, dgmmiv(y) div(L(0/1).(x1 x2) x3) vce(robust)
est store r1

* 对比 xtabond 命令
xtabond y L(0/1).(x1 x2) x3, lag(2) vce(robust)
est store r2
esttab r1 r2
```



##### 一阶差分估计量：所有解释变量内生+稳健型标准误

```stata
xtdpd L(0/1).(y x1 x2) x3 时间虚拟变量, dgmmiv(y x1 x2) div(x3 时间虚拟变量) vce(robust)
```

- 对于dgmmiv(x) 选项中的变量，L(2/.).x 均作为差分方程的工具变量
- 对于div(x) 选项中的变量，D.x 均作为差分方程的工具变量



##### 系统 GMM 估计量：所有解释变量外生

```stata
xtdpd y L(1/2).y L(0/1).(x1 x2) x3, dgmmiv(y) lgmmiv(n) div(L(0/1).(x1 x2) x3)
est store r1

xtdpdsys n L(0/1).(w ys) k, lag(2)
est store r2
esttab r1 r2  
```

对比：

- xtdpd 需要设定 L(1/2).y 来标明解释变量中包含 $y_{i(t-1)}$ 和 $y_{i(t-2)}$，而 xtdpdsys 通过选项 lag(2) 来进行该设定
- xtdpd 需要明确指定**差分方程的内生变量**（选项 dgmmiv() ），**水平方程的内生变量**（lgmmiv()）以及外生变量（选项 div()），而 xtdpdsys命令无需这些设定，在不附加 endog() 选项的情况下，默认把 $y_{i(t-1)}$ 和 $y_{i(t-2)}$ 视为内生变量



##### 系统 GMM 估计量：所有解释变量内生+两阶段估计

```stata
xtdpd L(0/1).(y x1 x2) 时间虚拟变量, dgmmiv(y x1 x2) lgmmiv(y x1 x2) div(时间虚拟变量) twostep
est store r1
estat abond
estat sargan
```



##### Sargan 检验拒绝原假设

```stata
xtdpd y L.y L(0/2).(x1 x2) x3 时间虚拟变量, div(L(0/1).(x1 x2) x3 时间虚拟变量) dgmmiv(y,lag(3 .)) hascons
	/* 注意此处的变化 */
estat sargan
```



### 4. xtlsdvc 命令（纠偏 LSDV 估计）

```stata
xtlsdvc y x1 x2 x3 时间虚拟变量, initial(ah)
est store r_AH82
xtlsdvc y x1 x2 x3 时间虚拟变量, initial(ab) 
est store r_AB91
xtlsdvc y x1 x2 x3 时间虚拟变量, initial(bb) 
est store r_BB98 

xtreg y L.y x1 x2 x3 时间虚拟变量, fe
est store r_FE

xtlsdvc y x1 x2 x3 时间虚拟变量, initial(ab) bias(3) vcov(10)
	/* bias()：精度，默认 1，最高 3 */
	/* vcov()：采用 Bootstrap 获得标准误，表示 Bootstrap 的次数 */
est store r_AB_bs

xtlsdvc n w k ys yr1980-yr1984, initial(ah) first lsdv
	/* 选项 first 呈现 IV 估计的结果 */
	/* 选项 lsdv 呈现 FE 的估计结果 */
	
* 对比结果
local mm "r_AH82 r_AB91 r_BB98 r_FE r_AB_bs"
esttab `mm', mtitle(`mm')
```

建议：

- ah：不建议适用

- ab：适用于一阶滞后项系数很小

- bb：适用于一阶滞后项系数接近 1





## 面板单位根检验

### Multivariate ADF based test

```stata
madfuller y, lags(滞后阶数)
```



### Individual ADF based tests

#### 1. pescadf 命令

```stata
pescadf y, lags(滞后阶数)
pescadf y, lags(滞后阶数) trend
	/* trend：包含时间趋势 */
pescadf y, lags(滞后阶数) trend trunoff
	/* trunoff: 对个别截面得到的统计量的离群值进行处理 */
```

> t-bar: 各个截面 ADF 检验得到的 t 值的平均值



#### 2. ipshin 命令*

```stata
ipshin y, lags(滞后阶数)
	/* 可以针对每个截面单独设计滞后阶数，如 lag(2 2 2 3 4 4 3 2) */
	/* nodemean：不删除每个截面的时间趋势效果 */
```



#### 3. xtfisher 命令

```stata
xtfisher y, lag(滞后阶数) trend
xtfisher y, lag(滞后阶数) pp
	/* 采用 PP 检验（默认是 ADF 检验） */
xtfisher y, lag(滞后阶数) display
	/* display：列出每个时间序列单独的检验结果 */
```



#### 4. levinlin 命令

```stata
levinlin y, lag(滞后阶数) trend
	/* 同样可单独列出每个截面的滞后阶数 */
```



#### 5. hadrilm 命令

```stata
hadrilm y
```

结果解释：

- Homo：假设每个截面的干扰项存在**同方差**
- Hetero：假设每个截面的干扰项存在**异方差**
- SerDep：假设每个截面存在**序列相关**





## 面板协整检验

### nharvey 命令：基于残差的检验

```stata
nharvey y
nharvey y, vlag(滞后阶数)
nharvey y, vlag(滞后阶数) trend
```



### xtwest 命令：基于误差修正模型的检验

```stata
xtwest y x, westerlund constant trend lags(1 3) leads(0 3) lrwindow(3) noisily
	/* westerlund: 呈现 Westerlund(2007)文中的结果 */
	/* constant: 附加常数项 */
	/* trend: 附加时间趋势项 */
	/* lags(1 3): 相当于 lag(1/3) */
	/* leads(0 3): 相当于 leads(-3/3) */
	/* lrwindow(3): 采用半参数方法计算 a_i 的标准误，指定 Bartlett 核函数的窗口大小 */
	/* noisily: 显示所有截面的回归结果 (针对每个截面做误差修正模型) */
```

lags(1 3) ：加入短期动态关系中 y 的滞后项的结构 $\Delta y_{i(t-1)},\ \Delta y_{i(t-2)},\ \Delta y_{i(t-3)}$ 

lead(0 3)：加入短期动态关系中 x 的滞后项的结构 $\Delta x_{i(t+1)},\ \Delta x_{i(t+2)},\ \Delta x_{i(t+1)},\ \Delta x_{i(t)},\ \Delta x_{i(t-1)},\ \Delta x_{i(t-2)},\ \Delta x_{i(t-3)}$ 

```stata
xtwest y x, constant trend lags(1) leads(1) lrwindow(3) bootstrap(500)
	/* 采用 Bootstrap 方法 */
```

> 推荐用 Bootstrap 方法





## 面板误差修正模型：xtpmg 命令

```stata
* 面板单位根检验
ipshin y, lags(3)
ipshin D.y, lags(3)
ipshin x1 , lags(3)
ipshin D.x1, lags(3)
ipshin x2 , lags(3)
ipshin D.x2, lags(3)
* 存在单位根过程，可以进行协整分析
```



### 建立误差修正模型

#### 1. 采用 PMG(Pooled Mean-Group) 估计量  (pmg 选项)

```stata
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ec) pmg
	/* ec()：长期均衡关系，ec(ec) 是为了更改显示名称，为了好看 */
est store pmg
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ErrorC)  pmg
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2 ec(ec) full replace pmg
	/* replace：如果已经有 ec 变量，则自动替换成新的 */
	/* full：把每个截面的结果都显示出来 */
```

结果解释：

ec：长期均衡关系

SR 中的 ec：长期调整速度的系数

SR 中其他项：短期调整关系



#### 2. 采用 MG(Mean-Group) 估计量  (mg 选项)

分别估计 N 个时间序列，取它们的平均值

```stata
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ec) replace mg
est store mg
```



#### 3. 采用固定效应动态面板估计量 (dfe 选项)

假设：长期误差修正系数和短期系数均为常数，不随个体改变

```stata
xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ec) replace dfe
est store dfe

xtpmg D.y D.x1 D.x2 , lr(L.y x1 x2) ec(ec) replace dfe cluster(截面变量)
	/* 允许截面间存在相关性 */
est store dfe_cluster 
```



#### 结果对比

```stata
esttab pmg mg dfe dfe_cluster, mtitle(pmg mg dfe dfe_cluster)
```





## 模型的筛选

三者之间，两两比较

```stata
hausman pmg mg
hausman dfe pmg
hausman dfe mg
```

> 若卡方值小于 0，倾向于拒绝原假设，即认为约束不成立